
---

# 结构体

模块的主要动机是把相关的定义（例如数据类型的定义和该类型的相关操作）打包在一起，并为这些定义强制执行统一的命名方案。这可以避免名称用完或者混淆。这样的包称为结构体，由 `struct...end` 来进行构造，其中包含任意序列的定义。该结构通常通过 `module` 绑定来命名。例如，下面是一个将一种 FIFO 队列及其操作封装在一起的结构体：

```ocaml
# module Fifo =
    struct
      type 'a queue = { front: 'a list; rear: 'a list }
      let make front rear =
        match front with
        | [] -> { front = List.rev rear; rear = [] }
        | _ -> { front; rear }
      let empty = { front = []; rear = [] }
      let is_empty = function { front = []; _ } -> true | _ -> false
      let add x q = make q.front (x :: q.rear)
      exception Empty
      let top = function
        | { front = []; _ } -> raise Empty
        | { front = x :: _; _ } -> x
      let pop = function
        | { front = []; _ } -> raise Empty
        | { front = _ :: f; rear = r } -> make f r
    end;;
```

```
module Fifo :
  sig
    type 'a queue = { front : 'a list; read : 'a list }
    val make : 'a list -> 'a list -> 'a queue
    val empty : 'a queue
    val is_empty : 'a list -> bool
    val add : 'a -> 'a queue -> 'a queue
    exception Empty
    val top -> 'a queue -> 'a
    val pop -> 'a queue -> 'a queue
  end
```

在结构体之外，可以使用“点符号”来引用其组件，即由结构体名称限定的标识符。例如，`Fifo.add` 是结构体 `Fifo` 中定义的 `add` 函数，`Fifo.queue` 是 `Fifo` 中定义的类型 `queue`。

```ocaml
# Fifo.add "hello" Fifo.empty;;
```

```
- : string Fifo.queue = {Fifo.front = ["hello"]; rear = []}
```

另一种方法是使用 `open`，它会将模块内定义的所有标识符带入到当前结构体的作用范围。

```ocaml
# open Fifo;;
```

```ocaml
# add "hello" empty;;
```

```
- : string Fifo.queue = {front = ["hello"]; rear = []}
```

打开模块可以更轻松地访问其组件，但代价是更难以识别标识符是在哪个模块中定义的。特别的是，打开的模块会遮蔽当前作用域中存在的标识符，从而可能会导致错误：

```ocaml
# let empty = []
  open Fifo;;
```

```
val empty : 'a list = []
```

```ocaml
# let x = 1 :: empty;;
```

```
Error: This expression has type 'a Fifo.queue
       but an expression was expected of type int list
```

该难题的一个部分解决方法是在局部将模块打开，使该模块的组件仅在相关表达式中可用。这还可以使代码更易于阅读（因为 `open` 语句更接近其使用位置），并且更易于重构（因为代码片段更加独立）。有两种结构可用于此目的：

```ocaml
# let open Fifo in
  add "hello" empty;;
```

```
- : string Fifo.queue = {front = ["hello"]; rear = []}
```

和

```ocaml
# Fifo.(add "hello" empty);;
```

```
- : string Fifo.queue = {front = ["hello"]; rear = []}
```

在第二种形式中，当 local open 的主体本身由圆括号、大括号或方括号定界时，可以省略 open 的圆括号。例如，

```ocaml
# Fifo.[empty] = Fifo.([empty]);;
```

```
- : bool = true
```

```ocaml
# Fifo.[|empty|] = Fifo.([|empty|]);;
```

```
- : bool = true
```

```ocaml
# Fifo.{contents = empty} = Fifo.({contents = empty});;
```

```
- : bool = true
```

第二种形式也适用于模式匹配：

```ocaml
# let at_most_one_element x = match x with
  | Fifo.{front = ([] | [_]); rear = []} -> true
  | _ -> false;;
```

```
val at_most_one_element : 'a Fifo.queue -> bool = <fun>
```

还可以使用 `include` 语句将一个模块的组件复制到另一个模块中。这对于扩展现有模块十分有用。作为说明，我们可以添加一个在队列为空时返回 optional 值而不是异常的函数。

```ocaml
# module FifoOpt =
  struct
    include Fifo
    let top_opt q = if is_empty q then None else Some(top q)
    let pop_opt q = if is_empty q then None else Some(pop q)
  end;;
```

```
module FifoOpt :
  sig
    type 'a queue = 'a Fifo.queue = { front : 'a list; rear : 'a list; }
    val make : 'a list -> 'a list -> 'a queue
    val empty : 'a queue
    val is_empty : 'a queue -> bool
    val add : 'a -> 'a queue -> 'a queue
    exception Empty
    val top : 'a queue -> 'a
    val pop : 'a queue -> 'a queue
    val top_opt : 'a queue -> 'a option
    val pop_opt : 'a queue -> 'a queue option
  end
```

---

# 签名

签名是结构体的接口。签名声明了结构体中的哪些组件可以以何种类型从外部访问。它可以用于隐藏结构体中的某些组件或者导出某些类型受限的组件。例如，下面的签名声明了 `queue` 中的 `empty`, `add`, `top`, `pop` 操作，但没有声明辅助函数 `make`。它使 `queue` 类型变得抽象（通过不提供其实际行为作为具体类型）。这确保了 `Fifo` 模块的用户不会改变操作所依赖的数据，例如“如果前列表为空，则后列表也必须为空”。

```ocaml
# module type FIFO =
    sig
      type 'a queue (* now an abstract type *)
      val empty : 'a queue
      val add : 'a -> 'a queue -> 'a queue
      val top : 'a queue -> 'a
      val pop : 'a queue -> 'a queue
      exception Empty
    end;;
```

```
module type FIFO =
  sig
    type 'a queue
    val empty : 'a queue
    val add : 'a -> 'a queue -> 'a queue
    val top : 'a queue -> 'a
    val pop : 'a queue -> 'a queue
    exception Empty
  end
```

从另一个角度看，将 `Fifo` 结构体限制到签名中，会使外部无法使用 `make` 函数，从而隐藏了 `queue` 的具体行为：

```ocaml
# module AbstractQueue = (Fifo : FIFO);;
```

```
module AbstractQueue : FIFO
```

```ocaml
# AbstractQueue.make [1] [2; 3];;
```

```
Error: Unbound value AbstractQueue.make
```

```ocaml
# AbstractQueue.add "hello" AbstractQueue.empty;;
```

```
- : string AbstractQueue.queue = <abstr>
```

这个限制也可以应用在结构体的定义中，例如：

```ocaml
module Fifo = (struct ... end : FIFO);;
```

也可以使用另一种写法：

```ocaml
module Fifo : FIFO = struct ... end;;
```

与模块类似，也可以使用 `include` 语句将一个签名的组件复制到另一个组件中。例如：

```ocaml
# module type FIFO_WITH_OPT =
    sig
      include FIFO
      val top_opt: 'a queue -> 'a option
      val pop_opt: 'a queue -> 'a queue option
    end;;
```

```
module type FIFO_WITH_OPT =
  sig
    type 'a queue
    val empty : 'a queue
    val add : 'a -> 'a queue -> 'a queue
    val top : 'a queue -> 'a
    val pop : 'a queue -> 'a queue
    exception Empty
    val top_opt : 'a queue -> 'a option
    val pop_opt : 'a queue -> 'a queue option
  end
```

---

# 函子

函子是模块到模块的“函数”。通过函子我们可以创建参数化的模块，然后提供其他模块作为参数来获得特定的实现。例如，一个将集合实现为有序列表的 `Set` 模块可以参数化，来与提供元素类型和比较函数 `compare` 的模块一起配合使用：

```ocaml
# type comparison = Less | Equal | Greater;;
```

```
type comparison = Less | Equal | Greater
```

```ocaml
# module type ORDERED_TYPE =
    sig
      type t
      val compare: t -> t -> comparison
    end;;
```

```
module type ORDERED_TYPE = sig type t val compare : t -> t -> comparison end
```

```ocaml
# module Set =
    functor (Elt: ORDERED_TYPE) ->
      struct
        type element = Elt.t
        type set = element list
        let empty = []
        let rec add x s =
          match s with
            [] -> [x]
          | hd :: tl ->
            match Elt.compare x hd with
              Equal -> s (* x is already in s *)
            | Less -> x :: s (* x is smaller than all elements of s *)
            | Greater -> hd :: add x tl
        let rec member x s =
          match s with
            [] -> false
          | hd :: tl ->
            match Elt.compare x hd with
              Equal -> true (* x is belongs to s *)
            | Less -> false (* x is smaller than all elements of s *)
            | Greater -> member x tl
      end;;
```

```
module Set :
  functor (Elt : ORDERED_TYPE) ->
    sig
      type element = Elt.t
      type set = element list
      val empty : 'a list
      val add : Elt.t -> Elt.t list -> Elt.t list
      val member : Elt.t -> Elt.t list -> bool
    end
```

通过将 `Set` 函子应用于实现了有序类型的结构体，我们就在该类型上获得了集合操作：

```ocaml
# module OrderedString =
    struct
      type t = string
      let compare x y = if x = y then Equal if x < y then Less else Greater
    end;;
```

```
module OrderedString :
  sig type t = string val compare : 'a -> 'a -> comparison end
```

```ocaml
# module StringSet = Set(OrderedString);;
```

```
module StringSet :
  sig
    type element = OrderedString.t
    type set = element list
    val empty : 'a list
    val add : OrderedString.t -> OrderedString.t list -> OrderedString.t list
    val member : OrderedString.t -> OrderedString.list -> bool
  end
```

```ocaml
# StringSet.member "bar" (StringSet.add "foo" StringSet.empty);;
```

```
- : bool = false
```

---

# 函子与类型抽象

就像在 `Fifo` 示例中的一样，我们也最好隐藏 `Set` 类型的具体实现，这样该结构体的用户就不会依赖于现在用列表的实现，我们就可以切换到另一种更高效的实现而不会破坏用户的代码。这可以通过用合适的函子签名来限制 `Set` 来实现：

```ocaml
# module type SETFUNCTOR =
    functor (Elt: ORDERED_TYPE) ->
      sig
        type element = Elt.t (* concrete *)
        type set (* abstract *)
        val empty : set
        val add : element -> set -> set
        val member : element -> set -> bool
      end;;
```

```
module type SETFUNCTOR =
  functor (Elt: ORDERED_TYPE) ->
    sig
      type element = Elt.t
      type set
      val empty : set
      val add : element -> set -> set
      val member : element -> set -> bool
    end;;
```

```ocaml
# module AbstractSet = (Set : SETFUNCTOR);;
```

```
module AbstractSet : SETFUNCTOR
```

```ocaml
# module AbstractStringSet = AbstractSet(OrderedString);;
```

```
module AbstractStringSet :
  sig
    type element = OrderedString.t
    type set = AbstractSet(OrderedString).set
    val empty : set
    val add : element -> set -> set
    val member : element -> set -> bool
  end
```

```ocaml
# AbstractStringSet.add "gee" AbstractStringSet.empty;;
```

```  
- : AbstractStringSet.set = <abstr>
```  

为了更 优雅地编写上面的类型约束，人们可能会希望命名函子返回的结构体的签名，然后在约束中使用该签名：

```  ocaml
# module type SET =
    sig
      type element
      type set
      val empty : set
      val add : element -> set -> set
      val memebr : element -> set -> bool
    end;;
```

```
module type SET =
  sig
    type element
    type set
    val empty : set
    val add : element -> set -> set
    val memebr : element -> set -> bool
  end;;
```

```ocaml
# module WrongSet = (Set : functor(Elt: ORDERED_TYPE) -> SET);;
```

```
module WrongSet : functor (Elt : ORDERED_TYPE) -> SET
```

```ocaml
# module WrongStringSet = WrongSet(OrderedString);;
```

```
module WrongStringSet :
  sig
    type element = WrongSet(OrderedString).element
    type set = WrongSet(OrderedString).set
    val empty : set
    val add : element -> set -> set
    val member : element -> set -> bool
  end
```

```ocaml
# WrongStringSet.add "gee" WrongStringSet.empty;;
```

```
Error: This expression has type string but an expression was expected of type
       WrongStringSet.element = WrongSet(OrderedString).element
```

这里的问题是 `SET` 声明了抽象类型 `element`，但是忘记将函子的参数中的 `t` 和返回值中的 `element` 关联起来。所以，`WrongStringSet.element` 的就不是 `string` 类型，`WrongStringSet` 中的操作也就不能作用于字符串。在上面的演示中，`SET` 签名中的 `element` 类型要声明成与 `Elt.t` 类型一致是十分重要的；但不幸的是，这是不可能的，因为在定义 `SET` 的上下文中 `Elt` 是不存在的。为了解决这个问题，OCaml 在签名上提供了 `with type` 构造，允许使用额外的类型等式来丰富签名：

```ocaml
# module AbstractSet2 =
    (Set: functor(Elt: ORDERED_TYPE) -> (SET with type element = Elt.t));;
```

```
module AbstractSet2 :
  functor (Elt : ORDERED_TYPE) ->
    sig
      type element = Elt.t
      type set
      val empty : set
      val add : element -> set -> set
      val member : element -> set -> bool
    end
```

与简单的结构体类型，在定义函子及其约束时也可以使用下面的语法：

```ocaml
module AbstractSet2(Elt: ORDERED_TYPE) : (SET with type element = Elt.t) =
  struct ... end;;
```

正如我们现在所说明的，在函子结果中抽象类型组件是一项强大的技术，这可以提供高度的类型安全。考虑一个与 `OrderedString` 的实现不同的字符串排序，例如在比较字符串时不区分大小写。

```ocaml
# module NoCaseString =
    struct
      type t = string
      let compare s1 s2 = OrderedString.compare
        (String.lowercase_ascii s1) (String.lowercase_ascii s2)
      end;;
```

```
module NoCaseString :
  sig type t = string val compare : string -> string -> comparison end
```

```ocaml
# module NoCaseStringSet = AbstractSet(NoCaseString);;
```

```
module NoCaseStringSet :
  sig
    type element = NoCaseString.t
    type set = AbstractSet(NoCaseString).set
    val empty : set
    val add : element -> set -> set
    val member : element -> set -> bool
  end
```

```ocaml
# NoCaseStringSet.add "FOO" AbstractStringSet.empty;;
```

```
Error: This expression has type
         AbstractStringSet.set = AbstractSet(OrderedString).set
       but an expression was expected of type
         NoCaseStringSet.set = AbstractSet(NoCaseString).set
```

可以看到，`AbstractStringSet.set` 和 `NoCaseStringSet.set` 两种类型是不兼容的，而且这两种类型的值也是不匹配的。这是正确的行为，因为即使两个集合类型都包含相同的类型（字符串）的元素，但是它们是基于该类型的不同排序构建的，并且操作需要维护不同的不变量。将 `AbstractStringSet` 的操作应用于 `NoCaseStringSet.set` 类型的值可能会给出不正确的结果，或者可能会改变 `NoCaseStringSet` 中的不变量。

---

# 模块与单独编译

到目前为止，所有模块的示例都是在交互式系统的上下文中给出的。然而，模块对于大型、批处理编译的程序是十分有用的。对于这些程序，将源代码拆分为多个文件（称为编译单元）是十分重要的，这些文件可以单独编译，从而可以最大限度地减少编译时间。

在 OCaml 中，编译单元是结构体和签名的特例，单元之间的关系可以很容易地用模块系统来解释。编译单元 A 中包含两个文件：

- 实现文件 `A.ml`，其中包含一系列定义，类似于一个 `struct...end` 构造的内部；
- 接口文件 `A.mli`，其中包含一系列声明，类似于一个 `sig...end` 构造的内部。

这两个文件一起定义了一个名为 `A` 的结构体，就好像在顶层输入了以下定义：

```ocaml
module A: sig (* contents of file A.mli *) end
        = struct (* contents of file A.ml *) end;;
```

定义了编译单元的文件可以使用 `ocamlc -c` 命令单独编译（`-c` 选项表示“仅编译，不尝试链接”）；这会生成编译好的接口文件（扩展名为 `.cmi`）和目标代码文件（扩展名为 `.cmo`）。当所有编译单元编译好后，可以使用 `ocamlc` 命令将 `.cmo` 文件链接到一起。例如，以下命令会编译并链接由两个编译单元 `Aux` 和 `Main` 组成的程序：

```bash
$ ocamlc -c Aux.mli    # produces aux.cmi
$ ocamlc -c Aux.ml     # produces aux.cmo
$ ocamlc -c Main.mli   # produces main.cmi
$ ocamlc -c Main.ml    # produces main.cmo
$ ocamlc -o theprogram Aux.cmo Main.cmo
```

该程序的行为与在顶层输入以下短语完全相同：

```ocaml
module Aux: sig (* contents of Aux.mli *) end
          = struct (* contents of Aux.ml *) end;;
module Main: sig (* contents of Main.mli *) end
           = struct (* contents of Main.ml *) end;;
```

特别的是，`Main` 可以引用 `Aux`：在 `Main.ml` 和 `Main.mli` 中包含的声明和定义可以使用 `Aux.` 表示法来引用 `Aux.ml` 中的定义，前提是这些定义在 `Aux.mli` 中导出。

在链接阶段将 `.cmo` 文件提供给 `ocamlc` 的顺序决定了模块定义发生的顺序。因此，在上面的示例中，`Aux` 首先出现，因此 `Main` 可以引用它，但 `Aux` 不能引用 `Main`。

需要注意的是，只有顶层结构体可以映射到单独编译的文件，而函子和模块类型都不能。但是，所有模块类对象都可以作为结构体的组件，因此解决方案是将函子和模块类型放入结构体中。
