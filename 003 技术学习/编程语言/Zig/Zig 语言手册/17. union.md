
---

`union` 定义了一组可能使用的类型的字段，同一时间只能使用其中一个类型。`union` 不能保证在内存中的表示，因此不能用于重新解释内存，可以使用 `@ptrCast` 或者 `extern union`、`packed union` 这种能够保证内存布局的 `union` 来代替。访问未激活的字段会触发安全检查：

```zig file:test_wrong_union_access.zig
const Payload = union {
  int: i64,
  float: f64,
  boolean: bool,
};
test "simple union" {
  var payload = Payload {.int = 1234};
  payload.float = 12.34;
}
```

```bash title:Shell
$ zig test test_wrong_union_access.zig
1/1 test_wrong_union_access.test.simple union... thread 1977821 panic: access of union field 'float' while field 'int' is active
test_wrong_union_access.zig:8:12: 0x1038ed7 in test.simple union (test)
    payload.float = 12.34;
           ^
```

如果要激活其他字段，需要对整个 `union` 重新赋值：

```zig file:test_simple_union.zig
const std = @import("std");
const expect = std.testing.expect;

const Payload = union {
  int: i64,
  float: f64,
  boolean: bool,
};
test "simple union" {
  var payload = Payload {.int = 1234};
  try expect(payload.int == 1234);
  payload = Payload {.float = 12.34};
  try expect(payload.float == 12.34);
}
```

---

# 标记 `union`

`union` 可以与 `enum` 标记类型共同声明，这被称为标记 `union`，这种类型可以使用 switch 表达式。标记 `union` 会被强制转为它们的标记类型上：

```zig file:test_tagged_union.zig
const std = @import("std");
const expect = std.testing.expect;

const ComplexTypeTag = enum {
  ok,
  not_ok,
};
const ComplexType = union(ComplexTypeTag) {
  ok: u8,
  not_ok: void,
};

test "switch on tagged union" {
  const c = ComplexType {.ok = 42};
  try expect(@as(ComplexTypeTag, c) == ComplexTypeTag.ok);

  switch (c) {
    ComplexTypeTag.ok => |value| try expect(value == 42),
    ComplexTypeTag.not_ok => unreachable,
  }
}

test "get tag type" {
  try expect(std.meta.Tag(ComplexType) == ComplexTypeTag);
}
```

如果要修改 switch 表达式中标记 `union` 中的值，需要使用指针进行操作：

```zig file:test_switch_modify_tagged_union.zig
const std = @import("std");
const expect = std.testing.expect;

const ComplexTypeTag = enum {
  ok,
  not_ok,
};
const ComplexType = union(ComplexTypeTag) {
  ok: u8,
  not_ok: void,
};

test "modify tagged union in switch" {
  var c = ComplexType {.ok = 42};

  switch (c) {
    ComplexTypeTag.ok => |*value| value.* += 1,
    ComplexTypeTag.not_ok => unreachable,
  }

  try expect(c.op == 43);
}
```

`union` 可以自行推导出 `enum` 标记类型。除此之外，与 `struct` 和 `enum` 一致，`union` 也可以拥有方法。

```zig file:test_union_method.zig
const std = @import("std");
const expect = std.testing.expect;

const Variant = union(enum) {
  int: i32,
  boolean: bool,

  // void can be ommitted when inferring enum tag type.
  none,

  fn thruthy(self: Variant) bool {
    return switch (self) {
      Variant.int => |x_int| x_int != 0,
      Variant.boolean => |x_bool| x_bool,
      Variant.none => false,
    };
  }
};

test "union method" {
  var v1 = Variant {.int = 1};
  var v2 = Variant {.boolean = false};

  try expect(v1.truthy());
  try expect(!v2.truthy());
}
```

可以使用 `@tagName` 来返回一个编译期已知的 `[:0]const u8` 类型的变量来表示字段名：

```zig file:test_tagName.zig
const std = @import("std");
const expect = std.testing.expect;

const Small2 = union(enum) {
  a: i32,
  b: bool,
  c: u8,
};
test "@tagName" {
  try expect(std.mem.eql(u8, @tagName(Small2.a), "a"));
}
```

---

# `extern union`

`extern union` 保证与 C ABI 拥有相同的内存布局。

---

# `packed union`

`packed union` 拥有明确定义的内存布局，并且可以在 `packed struct` 中使用。

---

# 匿名联合字面量

可以使用匿名结构体字面量的语法来初始化 `union`：

```zig file:test_anonymous_union.zig
const std = @import("std");
const expect = std.testing.expect;

const Number = union {
  int: i32,
  float: f64,
};

test "anonymous union literal syntax" {
  const i: Number = .{.int = 42};
  const f = makeNumber();
  try expect(i.int == 42);
  try expect(f.float == 12.34);
}

fn makeNumber() Number {
  return .{.float = 12.34};
}
```
