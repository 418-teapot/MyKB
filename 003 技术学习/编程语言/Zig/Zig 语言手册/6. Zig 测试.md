
---

在 `test` 声明中的代码可以确保行为满足预期：

```zig file:testing_introduction.zig
const std = @import("std");

test "expect addOne adds one to 41" {
  // The Standard Library contains useful functions to help create tests.
  // `expect` is a function that verifies its argument is true. It will
  // return an error if its argument is false to indicate a failure.
  // `try` is used to return an error to the test runner to notify it
  // that the test failed.
  try std.testing.expect(addOne(41) == 42);
}

test addOne {
  // A test name can also be written using an identifier.
  try std.testing.expect(addOne(41) == 42);
}

fn addOne(number: i32)  i32 {
  return number + 1;
}
```

`zig test` 是一个创建并运行测试构建的工具。默认情况下，它使用 Zig 标准库提供的 *default test runner* 作为 `main` 入口点来构建并运行可执行程序。*default test runner* 会将测试结果打印到标准错误流：

```bash title:Shell
$ zig test testing_introduction.zig
1/2 test.expect addOne adds one to 41... OK
2/2 decltest.addOne... OK
All 2 tests passed.
```

---

# 测试声明

Zig 测试使用 `test` 关键字进行声明，后跟可选的字符串字面量或者标志符作为测试名，最后是合法的 Zig 代码块。

按照惯例，未命名的测试只用于运行其他测试。

测试声明与函数类似：它们都具有返回类型和代码块。`test` 的返回类型是隐式的，为 `anyerror!void` 的 `Error Union Type`，并且该返回类型不能被更改。如果 Zig 源文件没有使用 `zig test` 工具进行构建，那么测试声明会被忽略。

测试声明可以写在被测试代码相同的文件中，也可以写入单独的源文件中。由于测试声明是顶级声明，因此它们的顺序无关紧要，可以写在被测试代码之前或者之后。

## 文档测试

使用标志符命名的测试声明是文档测试。该标志符必须要引用相同作用域里的另一个声明。文档测试与文档注释一样，是作为关联的声明的文档，并且会出现在生成的文档中。

---

# 测试失败

当测试返回 error 时，该测试被视为失败，错误报告会输出到标准错误流。

```zig file:testing_failure.zig
const std = @import("std");

test "expect this to fail" {
  try std.testing.expect(false);
}

test "expect this to succeed" {
  try std.testing.expect(true);
}
```

```bash title:Shell
$ zig test testing_failure.zig
1/2 test.expect this to fail... FAIL (TestUnexpectedResult)
...
    try std.testing.expect(false);
    ^
2/2 test.expect this to succeed... OK
1 passed; 0 skipped; 1 failed.
```

---

# 跳过测试

跳过测试的一种方法是使用 `zig test --test-filter [text]` 命令，这只会构建指定名称的测试，需要注意的是，这不会过滤掉未命名的测试。

当一个 `test` 返回 `error.SkipZigTest` 错误时，*default test runner* 会跳过该测试，所有测试运行结束后，会报告跳过的测试总数。

```zig file:testing_skip.zig
test "this will be skipped" {
  return error.SkipZigTest;
}
```

```bash title:Shell
$ zig test testing_skip.zig
1/1 test.this will be skipped... SKIP
0 passed; 1 skipped; 0 failed.
```

---

# 报告内存泄漏

当代码使用 Zig 标准库提供的 `std.testing.allocator` 申请内存时，*default test runner* 会报告其发现的内存泄漏。

```zig file:testing_detect_leak.zig
const std = @import("std");

test "detect leak" {
  var list = std.ArrayList(u21).init(std.testing.allocator);
  // missing `defer list.deinit();`
  try list.append('☔');

  try std.testing.expect(list.items.len == 1);
}
```

```bash title:Shell
$ zig test testing_detect_leak.zig
1/1 test.detect leak... OK
[gpa] (err): memory address 0x7f736a84a000 leaked:
...
All 1 tests passed.
1 errors were logged.
1 tests leaked memory.
```

---

# 检测测试构建

可以使用编译期变量 `@import("builtin").is_test` 来检测是否为测试。

```zig file:testing_detect_test.zig
const std = @import("std");
const builtin = @import("builtin");
const expect = std.testing.expect;

test "builtin.is_test" {
  try expect(isATest());
}

fn isATest() bool {
  return builtin.is_test;
}
```

```bash title:Shell
$ zig test testing_detect_test.zig
1/1 test.builtin.is_test... OK
All 1 tests passed.
```

---

# 测试输出与日志

*default test runner* 和 Zig 标准库的 `testing` 命名空间会将消息输出到标准错误流。

---

# `testing` 命名空间

Zig 标准库的 `testing` 命名空间中包含许多有用的函数，例如：

```zig file:testing_namespace.zig
const std = @import("std");

test "expectEqual demo" {
  const expected: i32 = 42;
  const actual = 42;

  // The first argument to `expectEqual` is the known, expected, result.
  // The second argument is the result of some expression.
  // The actual's type is casted to the type of expected.
  try std.testing.expectEqual(expected, actaul);
}

test "expectError demo" {
  const expected_error = error.DemoError;
  const actual_error_union: anyerror!void = error.DemoError;

  // `expectError` will fail when the actual error is different than
  // the expected error.
  try.std.testing.expectError(expected_error, actual_error_union);
}
```

```bash title:Shell
$ zig test testing_namespace.zig
1/2 test.expectEqual demo... OK
2/2 test.expectError demo... OK
All 2 tests passed.
```

---

# 测试工具

`zig test` 工具有一些影响编译的命令行参数，可以使用 `zig test --help` 获取完整列表。
