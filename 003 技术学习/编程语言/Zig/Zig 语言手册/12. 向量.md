
---

向量是一组能够并行执行的布尔值、整数、浮点数或者指针，向量会尽可能地使用 SIMD 指令。向量类型是通过内建函数 `@Vector` 创建的。

向量能够使用的内建运算符与其底层基本类型一致。这些操作会按元素执行，并返回与输入向量长度相同的向量。这些操作包括：

- 算数运算（`+`, `-`, `/`, `*`, `@divFloor`, `@sqrt`, `@ceil`, `@log`, 等）
- 按位运算（`>>`, `<<`, `&`, `|`, `~`, 等）
- 比较运算（`<`, `>`, `==`, 等）

禁止对标量和向量之间使用内建运算符。Zig 提供了 `@splat` 内建函数来将标量转为向量，`@reduce` 函数和数组索引语法来将向量转为标量。在编译器已知长度的情况下，向量还支持赋值给定长的数组，反之亦然。

Zig 提供了 `@shuffle` 和 `@select` 函数来改变向量内部和向量之间的元素排列。

如果向量操作的长度比目标机器的原生 SIMD 指令短，在该操作会被编译为一条 SIMD 指令，否则会被编译为多条。如果当前操作在目标体系结构上没有 SIMD 的支持，那么编译器会逐元素执行标量操作。尽管向量长度在 2 ~ 64 这种 2 的小幂会更典型，Zig 也支持最长为 $2^{32} - 1$ 的向量。需要注意的是，如果向量长度过长（例如 $2^{20}$），当前版本的 Zig 编译器可能会产生崩溃。

```zig file:test_vector.zig
const std = @import("std");
const expectEqual = std.testing.expectEqual;

test "Basic vector usage" {
  // Vectors have a compile-time known length and base type.
  const a = @Vector(4, i32) {1, 2, 3, 4};
  const b = @Vector(4, i32) {5, 6, 7, 8};

  // Math operations take place element-wise.
  const c = a + b;

  // Individual vector elements can be accessed using array indexing
  // syntax.
  try expectEqual(6, c[0]);
  try expectEqual(8, c[1]);
  try expectEqual(10, c[2]);
  try expectEqual(12, c[3]);
}

test "Conversion between vectors, arrays and slices" {
  // Vectors and fixed-length arrays can be automatically assigned
  // back and forth
  const arr1: [4]f32 = [_]f32 {1.1, 3.2, 4.5, 5.6};
  const vec: @Vector(4, f32) = arr1;
  const arr2: [4]f32 = vec;
  try expectEqual(arr1, arr2);

  // You can also assign from a slice with comptime-known kength to a
  // vector using .*
  const vec2: @Vector(2, f32) = arr1[1..3].*;

  const slice: []const f32 = &arr1;
  var offset: u32 = 1; // var to make it runtime-known
  _ = &offset; // suppress 'var is never mutated' error
  // To extract a comptime-known length from a runtime-known offset,
  // first extract a new slice from the starting offset, then an array
  // of comptime-known length
  const vec3: @Vector(2, f32) = slice[offset..][0..2].*;
  try expectEqual(slice[offset], vec2[0]);
  try expectEqual(slice[offset + 1], vec2[1]);
  try expectEqual(vec2, vec3);
}
```
