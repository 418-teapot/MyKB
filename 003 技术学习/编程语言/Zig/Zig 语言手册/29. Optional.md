
---

Zig 在不影响效率和可读性的前提下使用 optional 类型提供了安全性。可以使用 `?` 放在类型前声明一个 optional 类型。

```zig file:optional_integer.zig
// normal integer
const normal_int: i32 = 1234;

// optional integer
const optional_int: ?i32 = 5678;
```

变量 `optional_int` 可以是 `i32` 的数或者为 `null`。

对于指针来说，空引用是许多运行时异常的根源，甚至被指责为计算机科学中最严重的错误。Zig 没有这些错误。相反，可以使用 optional 指针。由于我们知道可以使用 0 作为 optional 指针的 null 值，所以 optional 指针会被秘密地编译为一个普通指针。

通常，不使用 null 的缺点是会是代码编写起来更加冗长。但是，让我们比较一些等效的 C 代码和 Zig 代码。

```c file:call_malloc_in_c.c
// malloc prototype included for reference
void *malloc(size_t size);

struct Foo *do_a_thing(void) {
  char *ptr = malloc(1234);
  if (!ptr) return NULL;
  // ...
}
```

```zig file:call_malloc_from_zig.zig
// malloc prototype included for reference
extern fn malloc(size: usize) ?[*]u8;

fn foAThing() ?*Foo {
  const ptr = malloc(1234) orelse return null;
  _ = ptr; // ...
}
```

在这里，Zig 至少与 C 一样方便。而且，`ptr` 的类型是 `[*]u8` 而不是 `?[*]u8`，`orelse` 类型解包了 optional 类型，保证了 `ptr` 在函数中任何地方的使用均为非空。

另一种检查 NULL 的形式如下所示：

```c file:checking_null_in_c.c
void do_a_thing(struct Foo *foo) {
  // do some stuff

  if (foo) {
    do_something_with(foo);
  }

  // do some stuff
}
```

在 Zig 中可以进行相同的操作：

```zig file:checking_null_in_zig.zig
const Foo = struct {};
fn doSomethingWithFoo(foo: *Foo) void {
  _ = foo;
}

fn doAThing(optional_foo: ?*Foo) void {
  // do some stuff

  if (optional_foo) |foo| {
    doSomethingWithFoo(foo);
  }

  // do some stuff
}
```

再次，值得注意的是，在 `if` 块内，`foo` 不再是一个 optional 指针，而是一个普通指针，其不能为 null。

这样做的一个好处是，采用普通指针作为参数的函数可以使用 GCC 中的 `__attribute__((nonnull))` 进行注解。当知道指针参数不会为 null 时，优化器又是可以做出更好的决策。

---

# optional 类型

可以通过在类型前声明 `?` 来创建一个 optional 类型。可以通过编译期反射来获得一个 optional 的子类型：

```zig file:test_optional_type.zig
const std = @import("std");
const expect = std.testing.expect;

test "optional type" {
  // Declare an optional and coerce from null:
  var foo: ?i32 = null;

  // Coerce from child type of an optional.
  foo = 1234;

  // Use compile-time reflection to access the child type of the
  // optional:
  try comptime expect(@typeInfo(@TypeOf(foo)).Optional.child == i32);
}
```

---

# `null`

就像 `undefined` 一样，`null` 有它自己的类型，使用它的唯一方法是将其转换为不同的类型：

```zig file:null.zig
const optional_value: ?i32 = null;
```

---

# optional 指针

可以保证 optional 指针与普通指针的大小是相同的。当 optional 指针为 `null` 时，保证其地址为 0。

```zig file:test_optional_pointer.zig
const std = @import("std");
const expect = std.testing.expect;

test "optional pointers" {
  // Pointers cannot be null. If you want a null pointer, use the
  // optional prefix `?` to make the pointer type optional.
  var ptr: ?*i32 = null;

  var x: i32 = 1;
  ptr = &x;

  try expect(ptr.?.* == 1);

  // Optional pointers are the same size as normal pointers, because
  // pointer value 0 is used as the null value.
  try expect(@sizeOf(?*i32) == @sizeOf(*i32));
}
```
