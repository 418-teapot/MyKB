
---

```zig file:test_functions.zig
const std = @import("std");
const builtin = @import("builtin");
const native_arch = builtin.cpu.arch;
const expect = std.testing.expect;

// Functions are declared like this:
fn add(a: i8, b: i8) i8 {
  if (a == 0) {
    return b;
  }
  return a + b;
}

// The export specifier make a function externally visible in the
// generated object file, and makes it use the C ABI.
export fn sub(a: i8, b: i8) i8 {
  return a - b;
}

// The extern specifier is used to declare a function that will be
// resolved at link time, when linking statically, or at runtime, when
// linking dynamically. The quoted identifier after the extern keyword
// specifies the library that has the function. (e.g. "c" -> libc.so)
// The callconv specifier changes the calling convention of the function.
const WINAPI: std.builtin.CallingConvention =
  if (native_arch == .x86) .Stdcall else .C;
extern "kernel32"
fn ExitProcess(exit_code: u32) callconv(WINAPI) noreturn;
extern "c" fn atan(a: f64, b: f64) f64;

// The @setCold builtin tells the optimizer that a function is rarely
// called.
fn abort() noreturn {
  @setCold(true);
  while (true) {}
}

// The naked calling convention makes a function not have any function
// prologue or epilogue. This can be useful when integrating with
// aeesmbly.
fn _start() callconv(.Naked) noreturn {
  abort();
}

// The inline calling convention forces a function to be inlined at all
// call sites. If the function cannot be inlined, it is a compile-time
// error.
inline fn shiftLeftOne(a: u32) u32 {
  return a << 1;
}

// The pub specifier allows the function to be visible when importing.
// Another file can use @import and call sub2.
pub fn sub2(a: i8, b: i8) i8 {
  return a - b;
}

// Function pointes are prefixed with `*const`.
const Call2Op = *const fn (a: i8, b: i8) i8;
fn doOp(fnCall: Call2Op, op1: i8, op2: i8) i8 {
  return fnCall(op1, op2);
}

test "function" {
  try expect(doOp(add, 5, 6) == 11);
  try expect(doOp(sub2, 5, 6) == -1)
}
```

函数体和函数指针是有区别的。函数体是仅限编译时的类型，而函数指针可能是运行时已知的。

---

# 按值传递参数

基本数据类型例如整型和浮点型，在作为参数进行传递时会被复制一份，函数体内部使用的是该副本。这被称为按值传递。复制基本数据类型是无代价的，只需要设置寄存器即可。

结构体、联合、数组使用按值传递时有可能代价会很高，由于 Zig 中参数必须不可变，所以当这些类型作为参数时，Zig 会自行选择使用按值传递还是按引用传递。

```zig file:test_pass_by_reference_or_value.zig
const std = @import("std");
const expect = std.testing.expect;

const Point = struct {
  x: i32,
  y: i32,
};

fn foo(point: Point) i32 {
  // Here, `point` could be a reference, or a copy. The function body
  // can ignore the difference and treat it as a value. Be very careful
  // taking the address of the parameter -- it should be treated as if
  // the address will become invalid when function returns.
  return point.x + point.y;
}

test "pass struct to function" {
  try expect(foo(Point {.x = 1, .y = 2}) == 3);
}
```

对于 `extern fn`，Zig 会按值 C ABI 标准按值传递结构体和联合。

---

# 函数参数类型推断

函数参数可以使用 `anytype` 来声明类型。在这种情况下，调用函数时将会自动推断参数类型。可以使用 `@TypeOf` 和 `@typeInfo` 来获取推断的类型。

```zig file:test_fn_type_inference.zig
const std = @import("std");
const expect = std.testing.expect;

fn addFortyTwo(x: anytype) @TypeOf(x) {
  return x + 42;
}

test "fn type inference" {
  try expect(addFortyTwo(1) == 43);
  try expect(@TypeOf(addFortyTwo(1)) == comptime_int);
  const y: i64 = 2;
  try expect(addFortyTwo(y) == 44);
  try expect(@TypeOf(y)) == i64);
}
```

---

# 内联函数

在函数定义的位置加上 `inline` 关键字会使得该函数在调用点语义上内联。与普通函数不同，在调用点处内联函数的参数会被视为编译时已知，并一直传播到函数的返回值处：

```zig file:inline_call.zig
test "inline function call" {
  if (foo(1200, 34) != 1234) {
    @compileError("bad");
  }
}

inline fn foo(a: i32, b: i32) i32 {
  return a + b;
}
```

当把该文件中的 `inline` 关键字移除后，该测试会触发编译错误从而失败。

通常情况下，让编译器自行决定是否内联是更好的选择，除非：

- 出于调试目的更改调用栈的栈帧；
- 强制将编译时已知的参数传播到返回值，例如上面的示例；
- 测试性能。

需要注意的是，内联实际上限制了编译器可以执行的操作。这可能会损害二进制大小、编译速度，甚至运行时性能。

---

# 函数反射

```zig file:test_fn_reflection.zig
const std = @import("std");
const math = std.math;
const testing = std.testing;
const expect = testing.expect;

test "fn relection" {
  try expect(@typeInfo(@TypeOf(expect)).Fn.params[0].type.? == bool);
  try expect(@typeInfo(@TypeOf(testing.tmpDir)).Fn.return_type.? ==
             testing.TmpDir);
  try expect(@typeInfo(@TypeOf(math.Log2Int)).Fn.is_generic);
}
```
