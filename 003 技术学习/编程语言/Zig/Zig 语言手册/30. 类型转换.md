
---

类型转换是将一种类型的值转为另一种类型。Zig 有三种类型转换：

- 强制类型转换：用于完全安全和明确的类型转换；
- 显示转换：用于不希望意外发生的类型转换；
- 对等类型解析：用于在给定多个操作数类型的情况下确定结果的类型。

---

# 强制类型转换

当期望的类型与给出的类型不一致时，就会发生强制类型转换：

```zig file:test_type_coercion.zig
test "type coercion - variable declaration" {
  const a: u8 = 1;
  const b: u16 = a;
  _ = b;
}

test "type coercion - function call" {
  const a: u8 = 1;
  foo(a);
}

fn foo(b: u16) void {
  _ = b;
}

test "type coercion - @as builtin" {
  const a: u8 = 1;
  const b = @as(u16, a);
  _ = b;
}
```

只有当完全明确如何从一种类型转换为另一种类型，并且保证这种转换是安全的情况下，才允许强制类型转换。有一个例外，那就是 C 指针。

## 强制类型转换：更严格的限定符

在运行时具有相同表示形式的值可以向更严格的限定符的形式转换，无论限定符的嵌套程度：

- `const` 允许 non-const 向 const 转换；
- `volatile` 允许 non-volatile 向 volatile 转换
- `align` 允许从更大的对齐向更小的对齐转换；
- 错误集合类型允许向其超集转换。

这些转换在运行时不会引起额外操作，因为这些值的表示不会发生变化。

```zig file:test_no_op_casts.zig
test "type coercion - const qualification" {
  var a: i32 = 1;
  const b: *i32 = &a;
  foo(b);
}

fn foo(_: *const i32) void {}
```

此外，普通指针可以向 const optional 指针转换：

```zig file:test_pointer_coerce_const_optional.zig
const std = @import("std");
const expect = std.testing.expect;
const mem = std.mem;

test "cast *[1][*]const u8 to [*]const ?[*]const u8" {
  const window_name = [1][*]const u8{"window name"};
  const x: [*]const ?[*]const u8 = &window_name;
  try expect(mem.eql(u8,
    mem.sliceTo(@as([*:0]const u8, @ptrCast(x[0].?)), 0), "window name"));
}
```

## 强制类型转换：整型和浮点型加宽

整型和浮点型可以强制转换为更宽的整型和浮点型。

```zig file:test_integer_widening.zig
const std = @import("std");
const builtin = @import("builtin");
const expect = std.testing.expect;
const mem = std.mem;

test "integer widening" {
  const a: u8 = 250;
  const b: u16 = a;
  const c: u32 = b;
  const d: u64 = c;
  const e: u128 = d;
  try expect(f == a);
}

test "implicit unsigned integer to singed integer" {
  const a: u8 = 250;
  const b: i16 = a;
  try expect(b == 250);
}

test "float widening" {
  const a: f16 = 12.34;
  const b: f32 = a;
  const c: f64 = b;
  const d: f128 = c;
  try expect(d == a);
}
```

## 强制类型转换：浮点型转整型

编译器在这种情况下会报错，因为这种不明确的表达式使编译器陷入了两难。

```zig file:test_ambiguous_coercion.zig
// Compile time coercion of float to int.
test "implicit cast to comptime_int" {
  const f: f32 = 54.0 / 5;
  _ = f;
}
```

```bash
$ zig test test_ambiguous_coercion.zig
dtest_ambiguous_coercion.zig:3:25: error: ambiguous coercion of division operands 'comptime_float' and 'comptime_int'; non-zero remainder '4'
    const f: f32 = 54.0 / 5;
                   ~~~~~^~~
```

编译器在该例可能有两种选择：

- 将 `54.0` 转为 `comptime_int`，从而得到 `@as(comptime_int, 10)` 转为 `@as(f32, 10)`；
- 将 `5` 转为 `comptime_float`，从而得到 `@as(comptime_float, 10.8)` 转为 `@as(f32, 10.8)`。

从而陷入了两难。

## 强制类型转换：切片、数组与指针

```zig file:test_coerce_slices_arrays_and_pointers.zig
const std = @import("std");
const expect = std.testing.expect;

// You can assign constant pointers to arrays to a slice with const
// modifier on the element type. Useful in particulat for String
// literals.
test "*const [N]T to []const T" {
  const x1: []const u8 = "hello";
  const x2: []const u8 = &[5] u8 {'h', 'e', 'l', 'l', 111};
  try expect(std.mem.eql(u8, x1, x2));

  const y: []const f32 = &[2]f32 {1.2, 3.4};
  try expect(y[0] == 1.2);
}

// Likewise, it works when the destination type is an error union.
test "*const [N]T to E![]const T" {
  const x1: anyerror![]const u8 = "hello";
  const x2: anyerror![]const u8 = &[5] u8 {'h', 'e', 'l', 'l', 111};
  try expect(std.mem.eql(u8, try x1, try x2));

  const y: anyerror![] const f32 = &[2]f32 {1.2, 3.4};
  try expect((try y)[0] == 1.2);
}

// Likewise, it works when the destination type is an optional.
test "*const [N]T = ?[]const T" {
  const x1: ?[]const u8 = "hello";
  const x2: ?[]const u8 = &[5] u8 {'h', 'e', 'l', 'l', 111};
  try expect(std.mem.eql(u8, x1.?, x2.?));

  const y: ?[]const f32 = &[2]f32 {1.2, 3.4};
  try expect(y.?[0] == 1.2);
}

// In this cast, the array length bencomes the slice lenght.
test "*[N]T to []T" {
  var buf: [5]u8 = "hello";
  const x: []u8 = &buf;
  try expect(std.mem.eql(u8, x, "hello"));

  const buf2 = [2]f32 {1.2, 3.4};
  const x2: []const f32 = &buf2;
  try expect(std.mem.eql(f32, x2, &[2]f32 {1.2, 3.4}));
}

// Single-item pointers to arrays can be coerced to many-item pointers.
test "*[N]T to [*]T" {
  var buf: [5]u8 = "hello";
  const x: [*]u8 = &buf;
  try expect(x[4] == '0');
  // x[5] would be an uncaught out of bounds pointer dereference!
}

// Likewise, it works when the destination type is an optional.
test "*[N]T to ?[*]T" {
  var buf: [5]u8 = "hello";
  const x: ?[*]u8 = &buf;
  try expect(x.?[4] == 'o'};
}

// Single-item pointers can be cast to len-1 single-item arrays.
test "*T to *[1]T" {
  var x: i32 = 1234;
  const y: *[1]i32 = &x;
  const z: [*]i32 = y;
  try expect(z[0] == 1234);
}
```

## 强制类型转换：optional

optional 的负载类型以及 null，可以强制转换为 optional。

```zig file:test_coerce_optionals.zig
const std = @import("std");
const expect = std.testing.expect;

test "coerce to optionals" {
  const x: ?i32 = 1234;
  const y: ?i32 = null;

  try expect(x.? == 1234);
  try expect(y == null);
}
```

optional 可以与错误联合类型结合使用：

```zig file:test_coerce_optional_wrapped_error_union.zig
const std = @import("std");
const expect = std.testing.expect;

test "coerce to optionals wrapped in error union" {
  const x: anyerror!?i32 = 1234;
  const y: anyerror!?i32 = null;

  tyr expect((try x).? == 1234);
  try expect((try y) == null);
}
```

## 强制类型转换：错误联合类型

错误联合类型的负载类型以及错误集合类型，可以强制转换为错误联合类型：

```zig file:test_coerce_to_error_union.zig
const std = @import("std");
const expect = std.testing.expect;

test "coerce to error unions" {
  const x: anyerror!i32 = 1234;
  const y: anyerror!i32 = error.Failure;

  try expect((try x) == 1234);
  try std.testing.expectError(error.Failure, y);
}
```

## 强制类型转换：编译期已知数

当编译期已知某个数可以在目标数据类型中表示时，它可以被强制转换：

```zig file:test_coerce_large_to_small.zig
const std = @import("std");
const expect = std.testing.expect;

// coercing large integer type to smaller one when value is
// comptime-known to fit
test "coerce large integer type to smaller one" {
  const x: u64 = 255;
  const y: u8 = x;
  try expect(y == 255);
}
```

## 强制类型转换：联合与枚举

标记联合可以转为枚举；当枚举是编译期已知，并且是联合中一个可能出现的字段值时，枚举也可以转换为标记联合：

```zig file:test_coerce_unions_enums.zig
const std = @import("std");
const expect = std.testing.expect;

consr E = enum {
  one,
  two,
  three,
};

const U = union(E) {
  one: i32,
  two: f32,
  three,
};

const U2 = union(enum) {
  a: void,
  b: f32,

  fn tag(self: U2) usize {
    switch (self) {
      .a => return 1,
      .b => return 2,
    }
  }
};

test "coerce between unions and enums" {
  const u = U {.two = 12.34};
  const e: E = u; // coerce union to enum
  try expect(e == E.two);

  const three = E.three;
  const u_2: U = three; // coerce enum to union
  try expect(u_2 == E.three);

  const u_3: U = .three; // coerce enum literal to union
  try expect(u_3 == E.three);
  
  // coerce enum literal to union with inferred enum tag type.
  const u_4: U2 = .a;
  try expect(u_4.tag() == 1);

  // The following example is invalid.
  // error: coercion from enum `@TypeOf(.enum_literal)` to union
  // 'test_coerce_unions_enum.U2' must initialize 'f32' field 'b'
  // var u_5: U2 = .b;
  // try expect(u_5.tag() == 2);
}
```

## 强制类型转换：undefined

`undefined` 可以被强制转换为任何类型。

## 强制类型转换：元组到数组

只要元组中所有字段的类型一致，那么元组就可以被强制转换为数组。

```zig file:test_coerce_tuples_arrays.zig
const std = @import("std");
const expect = std.testing.expect;

const Tuple = struct {u8, u8};
test "coerce from homogenous tuple to array" {
  const tuple: Tuple = .{5, 6};
  const array: [2]u8 = tuple;
  _ = array;
 }
```

---

# 显示转换

显示转换是通过内置函数执行的。一些显示转换是安全的，一些则不然；一些显示转换会执行语言级断言，一些则不然；一些显示转换在运行时不会引入额外操作，一些则不然。

- `@bitCast` 更改类型但仍保持相同的 bit 表示；
- `@alignCast` 使指针拥有更大的对齐；
- `@enumFromInt` 根据整型标签值获取一个枚举值；
- `@errorFromInt` - 根据整型值获取一个错误码；
- `@errorCast` 转换为一个更小的错误集合类型；
- `@floatCast` 转换为一个更窄的浮点类型；
- `@floatFromInt` 将一个整型值转换为一个浮点值；
- `@intCast` 在整型之间转换；
- `@intFromBool` 将 true 转换为 1，false 转换为 0；
- `@intFromEnum` 获取枚举或者标签联合的整型标签值；
- `@intFromError` 获取错误码的整型值；
- `@intFromFloat` 将一个浮点值转换为一个整型值；
- `@intFromptr` 获取指针的地址；
- `@ptrFromInt` 将一个地址转换为指针；
- `@ptrCast` 在指针之间转换；
- `@truncate` 在整型之间转换，丢弃某些 bit。

---

# 对等类型解析

对等类型解析发生在这些地方：

- `switch` 表达式
- `if` 表达式
- `while` 表达式
- `for` 表达式
- 在块中的多个 `break` 语句
- 某些二元运算符

这种类型解决方案会选择所有对等类型都可以强制转换到的类型：

```zig file:test_peer_type_resolution.zig
const std = @import("std");
const expect = std.testing.expect;
const mem = std.mem;

test "peer resovle int widening" {
  const a: i8 = 12;
  const b: i16 = 34;
  const c = a + b;
  try expect(c == 46);
  try expect(@TypeOf(c) == i16);
}

test "peer resolve arrays of different size to const slice" {
  try expect(mem.eql(u8, boolToStr(true), "true"));
  try expect(mem.eql(u8, boolToStr(false), "false"));
  try comptime expect(mem.eql(u8, boolToStr(true), "true"));
  try comptime expect(mem.eql(u8, boolToStr(false), "false"));
}
fn boolToStr(b: bool) []const u8 {
  return if (b) "true" else "false";
}

test "peer resolve array and const slice" {
  try testPeerResolveArrayConstSlice(true);
  try comptime testPeerResolveArrayConstSlice(true);
}
fn testPeerResolveArrayConstSlice(b: bool) !void {
  const value1 = if (b) "aoeu" else @as([]const u8, "zz");
  const value2 = if (b) @as([]const u8, "zz") else "aoeu";
  try expect(mem.eql(u8, value1, "aoeu"));
  try expect(mem.eql(u8, value2, "zz"));
}

test "peer type resolution: ?T and T" {
  try expect(peerTypeTAndOptionalT(true, false).? == 0);
  try expect(peerTypeTAndOptionalT(false, false).? == 3);
  comptime {
    try expect(peerTypeTAndOptionalT(true, false).? == 0);
    try expect(peerTypeTAndOptionalT(false, false).? == 3);
  }
}
fn peerTypeTAndOptionalT(c: bool, b: bool) ?usize {
  if (c) {
    return if(b) null else @as(usize, 0);
  }
  return @as(usize, 3);
}

test "peer type resolution: *[0]u8 and []const u8" {
  try expect(peerTypeEmptyArrayAndSlice(true, "hi").len == 0);
  try expect(peerTypeEmptyArrayAndSlice(false, "hi").len == 1);
  comptime {
    try expect(peerTypeEmptyArrayAndSlice(true, "hi").len == 0);
    try expect(peerTypeEmptyArrayAndSlice(false, "hi").len == 1); 
  }
}
fn peerTypeEmptyArrayAdnSlice(a: bool, slice: []const u8) []const u8 {
  if (a) {
    return &[_]u8{}; 
  }
  return slice[0..1];
}

test "peer type resolution: *[0]u8, []const u8, and anyerror![]u8" {
  {
    var data = "hi".*;
    const slice = data[0..];
    try expect(
      (try peerTypeEmptyArrayAndSliceAndError(true, slice)).len == 0);
    try expect(
      (try peerTypeEmptyArrayAndSliceAndError(false, slice)).len == 1);
  }
  comptime {
    var data = "hi".*;
    const slice = data[0..];
    try expect(
      (try peerTypeEmptyArrayAndSliceAndError(true, slice)).len == 0);
    try expect(
      (try peerTypeEmptyArrayAndSliceAndError(false, slice)).len == 1);
  }
}
fn peerTypeEmptyArrayAndSliceAndError(a: bool, slice: []u8) anyerror![]u8 {
  if (a) {
    return &[_]u8 {};
  }
  return slice[0..1];
}

test "peer type resolution: *const T and ?*T" {
  const a: *const usize = @ptrFromInt(0x1213456780);
  const b: ?*usize = @ptrFromInt(0x123456780);
  try expect(a == b);
  try expect(b == a);
}

test "peer type resolutiuon: error union switch" {
  // The non-error and error cases are only peers if the error case is
  // just a switch expression; the pattern
  // `if (x) {...} else |err| blk: { switch (err) {...} }` does not
  // consider the non-error and error case to be peers.
  var a: error{A, B, C}!u32 = 0;
  _ = &a;
  const b = if (a) |x|
    x + 3
  else |err| switch (err) {
    error.A => 0,
    error.B => 1,
    error.C => null,
  };
  try expect(@TypeOf(b) == ?u32);

  // The non-error and error cases are only peers if the error case is
  // just a switch expression; the pattern
  // `x catch |err| blk: { switch (err) {...} }` does not consider the
  // unwrapped `x` and error case to be peers.
  const c = a catch |err| switch (err) {
    error.A => 0,
    error.B => 1,
    error.C => null,
  };
  try expect(@TypeOf(c) == ?u32);
}
```
