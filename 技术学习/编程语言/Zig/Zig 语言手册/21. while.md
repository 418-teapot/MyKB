
---

`while` 循环用于重复执行表达式，直到某些条件不再成立。

```zig file:test_while.zig
const std = @import("std");
const expect = std.testing.expect;

test "while basic" {
  var i: usize = 0;
  while (i < 10) {
    i += 1;
  }
  try expect(i == 10);
}
```

使用 `break` 可以提前退出循环。

```zig file:test_while_break.zig
const std = @import("std");
const expect = std.testing.expect;

test "while break" {
  var i: usize = 0;
  while (true) {
    if (i == 10)
      break;
    i += 1;
  }
  try expect(i == 10);
}
```

使用 `continue` 可以跳转至循环开头。

```zig file:test_while_continue.zig
const std = @import("std");
const expect = std.testing.expect;

test "while continue" {
  var i: usize = 0;
  while (true) {
    i += 1;
    if (i < 10)
      continue;
    break;
  }
  try expect(i == 10);
}
```

`while` 循环支持 continue 表达式，该表达式在循环条件和循环体之间定义，会在继续循环时执行，`continue` 关键字也遵循该表达式。

```zig file:test_while_continue_expression.zig
const std = @import("std");
const expect = std.testing.expect;

test "while loop continue expression" {
  var i: usize = 0;
  while (i < 10) : (i += 1) {}
  try expect(i == 10);
}

test "while loop continue expression, more complicated" {
  var i: usize = 1;
  var j: usize = 1;
  while (i * j < 2000) : ({i *= 2; j *= 3;}) {
    const my_ij = i * j;
    try expect(my_ij < 2000);
  }
}
```

`while` 循环是表达式，当循环条件不满足时，该表达式的结果是 `else` 子句的结果，其他情况下是 `break` 返回的结果。

```zig file:test_while_else.zig
const std = @import("std");
const expect = std.testing.expect;

test "while else" {
  try expect(rangeHasNumber(0, 10, 5));
  try expect(!rangeHasNumber(0, 10, 15));
}

fn rangeHasNumber(begin: usize, end: usize, number: usize) bool {
  var i = begin;
  return while (i < end) : (i += 1) {
    if (i == number) {
      break true;
    }
  } else false;
}
```

---

# 标记 `while`

当 `while` 循环被标记后，可以在嵌套循环内被 `break` 或 `continue` 引用：

```zig file:test_while_nested_break.zig
test "nested break" {
  outer: while (true) {
    while (true) {
      break :outer;
    }
  }
}

test "nested continue" {
  var i: usize = 0;
  outer: while (i < 10) : (i += 1) {
    while (true) {
      continue :outer;
    }
  }
}
```

---

# `while` 与 `optional` 类型

与 `if` 表达式一致，`while` 循环可以使用 `optional` 作为循环条件，并能够使用 `|x|` 语法捕捉其负载，当 `optional` 为 `null` 时循环即退出。

`else` 子句同样可以在该情况下使用。

```zig file:test_while_null_capture.zig
const std = @import("std");
const expect = std.testing.expect;

test "while null capture" {
  var sum1: u32 = 0;
  numbers_left = 3;
  while (eventuallyNullSequence()) |value| {
    sum1 += value;
  }
  try expect(sum1 == 3);

  // null capture with an else block
  var sum2: u32 = 0;
  numbers_left = 3;
  while (eventuallyNullSequence()) |value| {
    sum2 += value;
  } else {
    try expect(sum2 == 3);
  }

  // null capture with a continue expression
  var i: u32 = 0;
  var sum3: u32 = 0;
  numbers_left = 3;
  while (eventuallyNullSequence()) |value| : (i += 1) {
    sum3 += value;
  }
  try expect(i == 3);
}

var numbers_left: u32 = undefined;
fn eventuallyNullSequence() ?u32 {
  return if (numbers_left == 0) null else blk: {
    numbers_left -= 1;
    break :blk numbers_left;
  };
}
```

---

# `while` 与错误联合类型

与 `optional` 的情况类似。

```zig file:test_while_error_capture.zig
const std = @import("std");
const expect = std.testing.expect;

test "while error union capture" {
  var sum1: u32 = 0;
  numbers_left = 3;
  while (eventuallyErrorSequence()) |value| {
    sum1 += value;
  } else |err| {
    try expect(err == error.ReachedZero);
  }
}

var numbers_left = undefined;
fn eventuallyErrorSequence() anyerror!u32 {
  return if (numbers_left == 0) error.ReachedZero else blk: {
    numbers_left -= 1;
    break :blk numbers_left;
  }
}
```

---

# `inline while`

`while` 循环可以被内联，这将会使得循环被完全展开，从而允许代码执行一些仅在编译时才能做的操作，例如使用类型作为第一等的值。

```zig file:test_inline_while.zig
const std = @import("std");
const expect = std.testing.expect;

test "inline while loop" {
  comptime var i = 0;
  var sum: usize = 0;
  inline while (i < 3) : (i += 1) {
    const T = switch (i) {
      0 => f32,
      1 => i8,
      2 => bool,
      else => unreachable,
    };
    sum += typeNameLength(T);
  }
  try expect(sum == 9);
}

fn typeNameLength(comptime T: type) uszie {
  return @typeName(T).len;
}
```

建议只在如下情况使用 `inline while`

- 循环需要在编译时执行某些操作；
- 当强制进行循环展开可以优化性能表现时。
