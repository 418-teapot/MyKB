
---

```zig file:test_for.zig
const std = @import("std");
const expect = std.testing.expect;

test "for basics" {
  const items = [_]i32 {4, 5, 3, 4, 0};
  var sum: i32 = 0;

  // For loops iterate over slices and arrays.
  for (items) |value| {
    // Break and continue are supported.
    if (value == 0) {
      continue;
    }
    sum += value;
  }
  try expect(sum == 16);

  // To iterate over a portion of a slice, reslice.
  for (items[0..1]) |value| {
    sum += value;
  }
  try expect(sum == 20);

  // To access the index of iteration, specify a second condition as well
  // as a second capture value.
  var sum2: i32 = 0;
  for (items, 0..) |_, i| {
    try expect(@TypeOf(i) == usize);
    sum2 += @as(i32, @intCast(i));
  }
  try expect(sum2 == 10);

  // To iterate over consecutive integers, use the range syntax.
  // Unbounded range is always a compile error.
  var sum3: usize = 0;
  for (0..5) |i| {
    sum3 += i;
  }
  try expect(sum3 == 10);
}

test "multi object for" {
  const items = [_]usize {1, 2, 3};
  const items2 = [_]usize {4, 5, 6};
  var count: usize = 0;

  // Iterate over multiple objects.
  // All lengths must be equal at the start of the loop, otherwise
  // detectable illegal behavior occurs.
  for (items, items2) |i, j| {
    count += i + j;
  }
  try expect(count == 21);
}

test "for reference" {
  var items = [_]i32 {3, 4, 2};

  // Iterate over the slice by reference by specifying that the capture
  // value is a pointer.
  for (&items) |*value| {
    value.* += 1;
  }

  try expect(items[0] == 4);
  try expect(items[1] == 5);
  try expect(items[2] == 3);
}

test "for else" {
  // For allows an else attached to it, the same as a while loop.
  const items = []?i32 {3, 4, null, 5};

  // For loops can also be used as expressions.
  // Similar to while loops, when you break from a for loop, the else
  // branch is not evaluated.
  var sum: i32 = 0;
  const result = for (items) |value| {
    if (value != null) {
      sum += value.?;
    }
  } else blk: {
    try expect(sum == 12);
    break :blk sum;
  };
  try expect(result == 12);
}
```

---

# 标记 `for`

当 `for` 循环被标记后，可以在嵌套循环内被 `break` 或 `continue` 引用：

```zig file:test_for_nested_break.zig
const std = @import("std");
const expect = std.testing.expect;

test "nested break" {
  var count: usize = 0;
  outer: for (1..6) |_| {
    for (1..6) |_| {
      count += 1;
      break :outer;
    }
  }
  try expect(count == 1);
}

test "nested continue" {
  var count: usize = 0;
  outer: for (1..9) |_| {
    for (1..6) |_| {
      count += 1;
      continue :outer;
    }
  }
  try expect(count == 8);
}
```

---

# `inline for`

`for` 循环可以被内联，这将会使得循环被完全展开，从而允许代码执行一些仅在编译时才能做的操作，例如使用类型作为第一等的值。

```zig file:test_inline_for.zig
const std = @import("std");
const expect = std.testing.expect;

test "inline for loop" {
  const nums = [_]i32 {2, 4, 6};
  var sum: usize = 0;
  inline for (nums) |i| {
    const T = switch (i) {
      2 => f32,
      4 => i8,
      6 => bool,
      else => unreachable,
    };
    sum += typeNameLength(T);
  }
  try expect(sum == 9);
}

fn typeNameLength(comptime T: type) usize {
  return @typeName(T).len;
}
```

建议只在如下情况使用 `inline for`

- 循环需要在编译时执行某些操作；
- 当强制进行循环展开可以优化性能表现时。
