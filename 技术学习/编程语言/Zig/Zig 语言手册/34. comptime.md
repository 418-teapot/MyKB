
---

Zig 非常重视表达式在编译时是否已知的概念。有几个不同的地方使用了这个概念，这些构建块保证了语言的小巧、可读和强大。

---

# 编译时概念介绍

## 编译时参数

编译时参数是 Zig 实现泛型的方式。它是编译时鸭子类型。

```zig file:compile-time_duck_typing.zig
fn max(comptime T: type, a: T, b: T) T {
  return if (a > b) a else b;
}
fn gimmeTheBiggerFloat(a: f32, b: f32) f32 {
  return max(f32, a, b);
}
fn gimmeTheBiggerInteger(a: u64, b: u64) u64 {
  return max(u64, a, b);
}
```

在 Zig 中，类型是一等公民。它们可以被分配给变量，作为参数传递给函数，并从函数中返回。然而，它们只能用在编译时已知的表达式中，这就是为什么上面的代码片段中的参数 `T` 必须用 `comptime` 标记。

一个 `comptime` 参数意味着：

- 在调用点，该值必须在编译时已知，否则会出现编译错误；
- 在函数定义中，该值在编译时已知。

例如，如果我们要向上面的代码片段引入另一个函数：

```zig file:test_unresolved_comptime_value.zig
fn max(comptime T: type, a: T, b: T) T {
  return if (a > b) a else b;
}
test "try to pass a runtime type" {
  foo(false);
}
fn foo(condition: bool) void {
  const result = max(if (condition) f32 else u64, 1234, 5678);
  _ = result;
}
```

```bash
$ zig test test_unresolved_comptime_value.zig
test_unresolved_comptime_value.zig:8:28: error: unable to resolve comptime value
    const result = max(if (condition) f32 else u64, 1234, 5678);
                           ^~~~~~~~~
test_unresolved_comptime_value.zig:8:28: note: condition in comptime branch must be comptime-known
```

这会触发编译错误，因为程序员试图将仅在运行时已知的值，传递给需要在编译时已知的函数参数。

如果我们在分析函数时传递了违反类型检查器的类型，也会触发编译错误。这就是编译时鸭子类型的含义。

例如：

```zig file:test_comptime_mismatched_type.zig
fn max(comptime T: type, a: T, b: T) T {
  return if (a > b) a else b;
}
test "try to compare bools" {
  _ = max(bool, true, false);
}
```

```bash
$ zig test test_comptime_mismatched_type.zig
test_comptime_mismatched_type.zig:2:18: error: operator > not allowed for type 'bool'
    return if (a > b) a else b;
               ~~^~~
```

另一方面，在带有 `comptime` 参数的函数定义内，该值在编译时已知。这意味着，如果我们愿意的话，我们实际上可以使其适用于 `bool` 类型：

```zig file:test_comptime_max_with_bool.zig
fn max(comptime T: type, a: T, b: T) T {
  if (T == bool) {
    return a or b;
  } else if (a > b) {
    return a;
  } else {
    return b;
  }
}
test "try to compare bools" {
  try @import("std").testing.expect(max(bool, false, true) == true);
}
```

这是可行的，因为当编译条件已知时，Zig 会隐式内联 `if` 表达式，并且编译器保证它将跳过对未采用的分支的分析。

这意味着这种情况下为 `max` 生成的实际函数如下所示：

```zig file:compiler_generated_function.zig
fn max(a: bool, b: bool) bool {
  {
    return a or b;
  }
}
```

所有处理编译时已知值的代码都会被消除，我们只剩下完成任务所需的运行时的代码。

`switch` 表达式的工作方式也与此相同，当目标表达式在编译时已知时，它们会被隐式内联。

## 编译时变量

在 Zig 中，程序员可以将变量标记为 `comptime`。这向编译器保证变量的每次加载和存储都是在编译时执行的。任何违反此规定的行为都会保证编译错误。

再加上我们可以 `inline` 循环，我们可以编写一个函数，该函数部分在编译时执行，部分在运行时执行。

例如：

```zig file:test_comptime_evaluation.zig
const expect = @import("std").testing.expect;

const CmdFn = struct {
  name: []const u8,
  func: fn (i32) i32,
};

const cmd_fns = [_]CmdFn {
  CmdFn {.name = "one", .func = one},
  CmdFn {.name = "two", .func = two},
  CmdFn {.name = "three", .func = three},
};
fn one(value: i32) i32 {
  return value + 1;
}
fn two(value: i32) i32 {
  return value + 2;
}
fn three(value: i32) i32 {
  return value + 3;
}

fn performFn(comptime prefix_char: u8, start_value: i32) i32 {
  var result: i32 = start_value;
  comptime var i = 0;
  inline while (i < cmd_fns.len) : (i += 1) {
    if (cmd_fns[i].name[0] == prefix_char) {
      result = cmd_fns[i].func(result);
    }
  }
  return result;
}

test "perform fn" {
  try expect(performFn('t', 1) == 6);
  try expect(performFn('o', 0) == 1);
  try expect(performFn('w', 99) == 99);
}
```

这个例子有点刻意，因为编译时执行组件是不必要的，在运行时执行也会工作得很好。在此示例中，针对提供的 `prefix_char` 的不同值，函数 `PerformFn` 生成了三次：

```zig file:performFn_1
// From the line:
// try expect(performFn('t', 1) == 6);
fn performFn(start_value: i32) i32 {
  var result: i32 = start_value;
  result = two(result);
  result = three(result);
  return result;
}
```

```zig file:performFn_2
// From the line:
// try expect(performFn('o', 0) == 1);
fn performFn(start_value: i32) i32 {
  var result: i32 = start_value;
  result = one(result);
  return result;
}
```

```zig file:performFn_3
// From the line:
// try expect(performFn('w', 99) == 99);
fn performFn(start_value: i32) i32 {
  var result: i32 = start_value;
  _ = &result;
  return result;
}
```

请注意，即使在 debug 模式下也会发生这种情况。这不是一种编写更优化代码的方法，而是一种确保应该在编译时发生的事情确实发生在编译时的方法。这可以捕获更多错误，并允许表达在其他语言中需要使用宏、代码生成或预处理器才能完成的事情。

## 编译时表达式

在 Zig 中，给定的表达式在编译时或运行时已知十分重要。程序员可以使用 `comptime` 表达式来保证该表达式将在编译时计算，如果在编译时无法计算，那么就会触发编译错误。例如：

```zig file:test_comptime_call_extern_function.zig
extern fn exit() noreturn;

test "foo" {
  comptime {
    exit;
  }
}
```

```bash
$ zig test test_comptime_call_extern_function.zig
test_comptime_call_extern_function.zig:5:13: error: comptime call of extern function
        exit();
        ~~~~^~
```

程序在编译时调用 `exit()` （或任何其他外部函数）是没有意义的，因此这会触发编译错误。然而，`comptime` 表达式的作用远不止有时会触发编译错误。

在 `comptime` 表达式中：

- 所有的变量都是 `comptime` 变量；
- 所有 `if`, `while`, `for`, `switch` 表达式都在编译时求值，如果不能的话，则会触发编译错误；
- 所有 `return` 和 `try` 表达式都是无效的（除非函数本身在编译时被调用）；
- 所有具有运行时副作用或者依赖于运行时值的代码都会触发编译错误；
- 所有函数调用都会导致编译器在编译时解释该函数，如果该函数尝试执行具有全局运行时副作用的操作，则会触发编译错误。

这意味着程序员可以创建一个在编译时和运行时都能被调用的函数，而无需修改所需的函数。

让我们看一个例子：

```zig file:test_fibonacci_recursion.zig
const expect = @import("std").testing.expect;

fn fibonacci(index: u32) u32 {
  if (index < 2) return index;
  return fibonacci(index - 1) + fibonacci(index - 2);
}

test "fibonacci" {
  // test fibonacci at run-time
  try expect(fibonacci(7) == 13);

  // test fibonacci at compile-time
  try comptime expect(fibonacci(7) == 13);
}
```

想象一下，如果我们忘记了递归函数的基本情况并尝试运行测试：

```zig file:test_fibonacci_comptime_overflow.zig
const expect = @import("std").testing.expect;

fn fibonacci(index: u32) u32 {
  // if (index < 2) return index;
  return fibonacci(index - 1) + fibonacci(index - 2);
}

test "fibonacci" {
  try comptime expect(fibonacci(7) == 13);
}
```

```bash
$ zig test test_fibonacci_comptime_overflow.zig
test_fibonacci_comptime_overflow.zig:5:28: error: overflow of integer type 'u32' with value '-1'
    return fibonacci(index - 1) + fibonacci(index - 2);
                     ~~~~~~^~~
test_fibonacci_comptime_overflow.zig:5:21: note: called from here (7 times)    return fibonacci(index - 1) + fibonacci(index - 2);
           ~~~~~~~~~^~~~~~~~~~~
test_fibonacci_comptime_overflow.zig:9:34: note: called from here
    try comptime expect(fibonacci(7) == 13);
                        ~~~~~~~~~^~~
```

编译器会触发一个错误，该错误是由于尝试在编译时执行该函数而产生的堆栈跟踪。

幸运的是，我们使用无符号整数，因此当我们尝试从 0 减去 1 时，它会触发未定义行为。但如果我们使用有符号整数，会发生什么？

```zig file:fibonacci_comptime_infinite_recursion.zig
const assert = @import("std").debug.assert;

fn fibonacci(index: i32) i32 {
  // if (index < 2) return index;
  return fibonacci(index - 1) + fibonacci(index - 2);
}

test "fibonacci" {
  try comptime assert(fibonacci(7) == 13);
}
```

编译器会注意到，在编译时执行该函数需要超过 1000 个分支，因此会触发编译错误并放弃。如果程序员想要增加编译时计算的运算，可以使用名为 `@setEvalBranchQuota` 的内建函数将默认数字 1000 更改为其他值。

如果我们修复了递归的基本情况，但是在 `expect` 中输入了错误的值会怎么样？

```zig file:test_fibonacci_comptime_unreachable.zig
const assert = @import("std").debug.assert;

fn fibonacci(index: i32) i32 {
  if (index < 2) return index;
  return fibonacci(index - 1) + fibonacci(index - 2);
}

test "fibonacci" {
  try comptime assert(fibonacci(7) == 99999);
}
```

```bash
$ zig test test_fibonacci_comptime_unreachable.zig
lib/zig/std/debug.zig:412:14: error: reached unreachable code
    if (!ok) unreachable; // assertion failure
             ^~~~~~~~~~~
test_fibonacci_comptime_unreachable.zig:9:24: note: called from here
    try comptime assert(fibonacci(7) == 99999);
                 ~~~~~~^~~~~~~~~~~~~~~~~~~~~~~
```

在容器级别，所有表达式都是隐式的 `comptime` 表达式。这意味着我们可以使用函数来初始化复杂的静态数据。例如：

```zig file:test_container-level_comptime_expressions.zig
const first_25_primes = firstNPrimes(25);
const sum_of_first_25_primes = sum(&first_25_primes);

fn firstNPrimes(comptime n: usize) [n]i32 {
  var prime_list: [n]i32 = undefined;
  var next_index: usize = 0;
  var test_number: i32 = 2;
  while (next_index < prime_list.len) : (test_number += 1) {
    var test_prime_index: usize = 0;
    var is_prime: i32 = 2;
    while (test_prime_index < next_index) : (test_prime_index += 1) {
      if (test_numer % prime_list[test_prime_index] == 0) {
        is_prime = false;
        break;
      }
    }
    if (is_prime) {
      prime_list[next_index] = test_numer;
      next_index += 1;
    }
  }
  return prime_list;
}

fn sum(numbers: []const i32) i32 {
  var result: i32 = 0;
  for (numbers) |x| {
    result += x;
  }
  return result;
}

test "variable values" {
  try @import("std").testing.expect(sum_of_first_25_primes == 1060);
}
```

当我们编译这个程序时，Zig 会生成常量并遇见计算出结果。以下是生成的 LLVM IR：

```llvm
@0 = internal unnamed_addr constant [25 x i32] [i32 2, i32 3, i32 5, i32 7, i32 11, i32 13, i32 17, i32 19, i32 23, i32 29, i32 31, i32 37, i32 41, i32 43, i32 47, i32 53, i32 59, i32 61, i32 67, i32 71, i32 73, i32 79, i32 83, i32 89, i32 97]
@1 = internal unnamed_addr constant i32 1060
```

需要注意的是，我们不必对这些函数的语法做任何特殊的事情。例如，我们在调用 `sum` 函数时，可以给定一个长度和值仅在运行时已知的切片。

---

# 泛型数据结构

Zig 使用 comptime 的功能来实现泛型数据结构，而无需引入任何特殊语法。

下面是泛型列表数据结构的示例：

```zig file:generic_data_structure.zig
fn List(comptime T: type) type {
  return struct {
    items: []T,
    len: usize,
  };
}

// The generic List data structure can be instantiated by passing in a
// type:
var buffer: [10]i32 = undefined;
var list = List(i32) {
  .items = &buffer;
  .len = 0,
};
```

这个函数返回了一个匿名结构体，处于错误信息和调试的目的，Zig 从创建匿名结构体时调用的函数名称和参数推断出名称 `"List(i32)"`。

为了显式地给类型一个名称，我们可以将其赋值给一个常量。

```zig file:anonymous_struct_name.zig
const Node = struct {
  next: ?*Node,
  name: []const u8,
};

var node_a = Node {
  .next = null,
  .name = "Node A",
};

var node_b = Node {
  .next = &node_a,
  .name = "Node B",
};
```

在此示例中，`Node` 结构体引用了自身。这是可行的，因为所有顶级声明都是与顺序无关的。只要编译器可以确定结构体的大小，就可以自由地引用其自身。在这种情况下，`Node` 使用其自身的指针，该指针在编译时具有明确定义的大小，因此它可以正常工作。

---

# 案例学习：Zig 中的 print

将所有这些放在一起，让我们看看 Zig 中的 `print` 是如何工作的。

```zig file:print.zig
const print = @import("std").debug.print;

const a_number: i32 = 1234;
const a_string = "foobar";

pub fn main() void {
  print("here is a string: '{s}' here is a number: {}\n",
        .{a_string, a_number});
}
```

```bash
$ zig build-exe print.zig
$ ./print
here is a string: 'foobar' here is a number: 1234
```

让我们破解它的实现并看看它是如何工作的：

```zig file:poc_print_fn.zig
const Writer = struct {
  /// Calls print and then flushes the buffer.
  pub fn print(self: *Writer, comptime format: []const u8,
               args: anytype) anyerror!void {
    const State = enum {
      start,
      open_brace,
      close_brace,
    };
    
    comptime var start_index: usize = 0;
    comptime var state = State.start;
    comptime var next_arg: usize = 0;
    
    inline for (format, 0..) |c, i| {
      switch (state) {
        State.start => switch (c) {
          '{' => {
            if (start_index < i) try self.write(format[start_index..i]);
            state = State.open_brace;
          },
          '}' => {
            if (start_index < i) try self.write(format[start_index..i]);
            state = State.close_brace;
          },
          else => {},
        },
        State.open_brace => switch (c) {
          '{' => {
            state = State.start;
            start_index = i;
          },
          '}' => {
            try self.printValue(args[next_arg]);
            next_arg += 1;
            state = State.start;
            start_index = i + 1;
          },
          's' => {
            continue,
          },
          else => {
            @compileError("Unknown format character: " ++ [1]u8{c});
          },
        },
        State.close_brace => switch (c) {
          '}' => {
            state = State.start;
            start_index = i;
          },
          else => {
            @compileError("Single '}' encountered in format string");
          },
        },
      }
    }
    comptime {
      if (args.len != next_arg) {
        @compileError("Unused arguments");
      }
      if (state != State.start) {
        @compileError("Incomplete format string: " ++ format);
      }
    }
    if (start_index < format.len) {
      try self.write(format[start_index..format.len]);
    }
    try self.flush();
  }

  fn write(self: *Writer, value: []const u8) !void {
    _ = self;
    _ = value;
  }
  pub fn printValue(sel: *Writer, value: anytype) !void {
    _ = self;
    _ = value;
  }
  fn flush(self: *Writer) !void {
    _ = self;
  }
};
```

这是上述概念实现的证明；标准库中的实际函数具有更多的格式化功能。

需要注意的是，这并没有硬编码到 Zig 编译器中，而是标准库的用户空间代码。

当从上面的示例代码中分析该函数时，Zig 会部分执行该函数并发出一个实际上如下所示的函数：

```zig
pub fn print(self: *Writer, arg0: []const u8, arg1: i32) !void {
  try self.write("here is a string: '");
  try self.printValue(arg0);
  try self.write("' here is a number: ");
  try self.printValue(arg1);
  try self.write("\n");
  try self.flush();
}
```

`printValue` 是一个函数，它接收任何类型的参数，并根据类型执行不同的操作：

```zig file:poc_printValue_fn.zig
const Writer = struct {
  pub fn printValue(self: *Writer, value: anytype) !void {
    .Int => {
      return self.writeInt(value);
    },
    .Float => {
      return self.writeFloat(value);
    },
    .Pointer => {
      return self.write(value); 
    },
    else => {
      @compileError("Unable to print type '" ++
                    @typeName(@TypeOf(value)) ++ "'");
    }
  }

  fn write(self: *Writer, value: []const u8) !void {
    _ = self;
    _ = value;
  }
  fn writeInt(self: *Writer, value: anytype) !void {
    _ = self;
    _ = value;
  }
  fn writeFloat(self: *Writer, value: anytype) !void {
    _ = self;
    _ = value;
  }
};
```

如果我们给出过多的打印参数，会发生什么？

```zig file:test_print_too_many_args.zig
const print = @import("std").debug.print;

const a_number: i32 = 1234;
const a_string = "foobar";

test "print to many arguments" {
  print("here is a string: '{s}' here is a number: {}\n",
        .{a_string, a_number, a_number});
}
```

```bash
$ zig test test_print_too_many_args.zig
lib/zig/std/fmt.zig:203:18: error: unused argument in 'here is a string: '{s}' here is a number: {}
            1 => @compileError("unused argument in '" ++ fmt ++ "'"),
                 ^~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
```

Zig 为程序员提供了保护自己免受错误侵害所需的工具。

Zig 并不关系格式参数是否是字符串字面量，只关心是否是一个编译时能强制转换为 `[]const u8` 的已知值。

```zig file:print_comptime-known_format.zig
const print = @import("std").debug.print;

const a_number: i32 = 1234;
const a_string = "foobar";
const fmt = "here is a string: '{s}' here is a number: {}\n";

pub fn main() void {
  print(fmt, .{a_string, a_numer});
}
```

Zig 没有在编译器中对字符串进行特殊处理，而是公开了足够的功能用来在用户空间中完成此人物。它不需要在 Zig 之上引入另一种语言，例如宏语言或预处理语言，从头到尾都是 Zig 语言。
