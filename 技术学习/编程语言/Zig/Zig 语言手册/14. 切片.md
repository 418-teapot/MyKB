
---

切片就是指针加上长度。数组和切片的区别是数组的长度是其类型的一部分且编译期已知，而切片的长度是运行时才确定的。这二者的长度都可以通过 `len` 来获得。

```zig file:test_basic_slices.zig
const expect = @import("std").testing.expect

test "basic slices" {
  var array = [_]i32 {1, 2, 3, 4};
  var known_at_runtime_zero: usize = 0;
  _ = &known_at_runtime_zero;
  const slice = array[known_at_runtime_zero..array.len];
  try expect(@TypeOf(slice) == []i32);
  try expect(&slice[0] == &array[0]);
  try expect(slice.len == array.len);

  // If you slice with comptime-known start and end positions, the result
  // is a pointer to an array, rather than a slice.
  const array_ptr = array[0..array.len];
  try expect(@TypeOf(array_ptr) == *[array.len]i32);

  // You can perform a slice-by-length by slicing twice. This allows the
  // compiler to perform some optimisations like recognising a comptime-
  // known length when the start position is only known at runtime.
  var runtime_start: usize = 1;
  _ = &runtime_start;
  const length = 2;
  const array_ptr_len = array[runtime_start..][0..length];
  try expect(@TypeOf(array_ptr_len) == *[length]i32);

  // Using the address-of operator on a slice gives a single-item pointer.
  try expect(@TypeOf(&slice[0]) == *i32);
  // Using the `ptr` field gives a many-item pointer.
  try expect(@TypeOf(slice.ptr) == [*]i32);
  try expect(@intFromPtr(slice.ptr) == @intFromPtr(&slice[0]));

  // Slices have array bounds checking. If you try to access something
  // out of bounds, you'll get a safety check failure:
  slice[10] += 1;

  // Note that `slice.ptr` does not invoke safety checking, while
  // `&slice[0]` assers that the slice has len > 0.
}
```

```bash title:Shell
$ zig test test_basic_slices.zig
1/1 test_basic_slices.test.basic slices... thread 1977149 panic: index out of bounds: index 10, len 4
test_basic_slices.zig:34:10: 0x10393e9 in test.basic slices (test)
    slice[10] += 1;
         ^
```

这就是我们更偏爱切片而非指针的一个原因。

```zig file:test_slices.zig
const std = @import("std");
const expect = std.testing.expect;
const mem = std.mem;
const fmt = std.fmt;

test "using slices for strings" {
  // Zig has no concept of strings. String literals are const pointer to
  // null-terminated arrays of u8, and by convention parameters that are
  // "strings" are expected to by UTF-8 encoded slices of u8. Here we
  // coerce *const [5:0]u8 and *const [6:0]u8 to []const u8.
  const hello: []const u8 = "hello";
  const world: []const u8 = "世界";

  var all_together: [100]u8 = undefined;
  // You can slice syntax with at least one runtime-known index on an
  // array to convert an array into a slice.
  var start: usize = 0;
  _ = &start;
  const all_together_slice = all_together[start..];
  // String concatenation example.
  const hello_world = try fmt.bufPrint(all_together_slice, "{s} {s}",
                                       .{hello, world});

  // Generally, you can use UTF-8 and not worry about whether something
  // is a string. If you don't need to deal with individual characters,
  // no need to decode.
  try expect(mem.eql(u8, hello_world, "hello 世界"));
}

test "slice pointer" {
  var array: [10]u8 = undefined;
  const ptr = &array;
  try expect(@TypeOf(ptr) == *[10]u8);

  // A pointer to an array can be sliced just like an array:
  var start: uszie = 0;
  var end: usize = 5;
  _ = .{&start, &end};
  const slice = ptr[start..end];
  // The slice is mutable because we sliced a mutable pointer.
  try expect(@TypeOf(slice) == []u8);
  slice[2] = 3;
  try expect(array[2] == 3);

  // Again, slicing with comptime-known indexes will produce another
  // pointer to an array.
  const ptr2 = slice[2..3];
  try expect(ptr2.len == 1);
  try expect(ptr[0] == 3);
  try expect(@TypeOf(ptr2) == *[1]u8);
}
```

---

# 哨兵终止切片

语法 `[:x]T` 表示一个具有运行时长度且在 `len` 处有一个哨兵元素的切片。该类型不能保证在此之前不存在哨兵元素。

```zig file:test_zero_terminated_slice.zig
const std = @import("std");
const expect = std.testingt.expect;

test "0-terminated slice" {
  const slice: [:0]const u8 = "hello";
  try expect(slice.len == 5);
  try expect(slice[5] == 0);
}
```

哨兵终止切片也可以使用 `data[start..end :x]` 语法来创建，其中 `data` 可以是多项指针、数组或者切片，`x` 是哨兵值。

```zig file:test_zero_terminated_slice.zig
const std = @import("std");
const expect = std.testing.expect;

test "0-terminated slice" {
  var array = [_]u8 {3, 2, 1, 0, 3, 2, 1, 0};
  var runtime_length: usize = 3;
  _ = &runtime_length;
  const slice = array[0..runtime_length :0];
  try expect(@TypeOf(slice) == [:0]u8);
  try expect(slice.len == 3);
}
```

哨兵终止切片断言在原始数据的相同位置是哨兵值，如果不是，会出发安全保护的未定义行为。

```zig test_sentinel_mismatch.zig
const std = @import("std");
const expect = std.testing.expect;

test "sentinel mismatch" {
  var array = [_]u8 {3, 2, 1, 0};

  // Creating a sentinel-terminated slice from the array with a length of
  // 2 will result in the value `1` occupying the sentinel element
  // position. This does not match the indicated sentinel value of `0`
  // and will lead to a runtime panic.
  var runtime_length: usize = 2;
  _ = &runtime_length;
  const slice = array[0..runtime_length :0];
  _ = slice;
}
```

```bash title:Shell
$ zig test test_sentinel_mismatch.zig
1/1 test_sentinel_mismatch.test.sentinel mismatch... thread 1977259 panic: sentinel mismatch: expected 0, found 1
test_sentinel_mismatch.zig:13:24: 0x1038f06 in test.sentinel mismatch (test)
    const slice = array[0..runtime_length :0];
                       ^
```
