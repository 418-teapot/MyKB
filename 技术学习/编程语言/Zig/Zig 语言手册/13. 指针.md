
---

Zig 有两种类型的指针：单项指针（single-item）和多项指针（many-item）。

- `*T` —— 单项指针指向单个元素。
  - 支持解引用语法：`ptr.*`
- `[*]T` —— 多项指针指向未知数量的多个元素。
  - 支持索引语法：`ptr[i]`
  - 支持切片语法：`ptr[start..end]` 和 `ptr[start..]`
  - 支持指针算数运算：`ptr + x` 和 `ptr - x`
  - 类型 `T` 必须有一个确定的大小，这意味着 `T` 不能为 `anyopaque` 类型或其他任意的 `opaque` 类型

这些类型与数组和切片紧密相关：

- `*[N]T` —— 指向 N 个元素的指针，也可以当作指向一个数组的单项指针。
  - 支持索引语法：`array_ptr[i]`
  - 支持切片语法：`array_ptr[start..end]`
  - 支持长度属性：`array_ptr.len`
- `[]T` —— 切片（胖指针，包含一个 `[*]T` 类型的指针和一个长度）
  - 支持索引语法：`slice[i]`
  - 支持切片语法：`slice[start..end]`
  - 支持长度属性：`slice.len`

可以使用 `&x` 来获取单项指针：

```zig file:test_single_item_pointer.zig
const expect = @import("std").testing.expect;

test "address of syntax" {
  // Get the address of a variable:
  const x: i32 = 1234;
  const x_ptr = &x;

  // Dereference a pointer:
  try expect(x_ptr.* == 1234);

  // When you get the address of a const variable, you get a const
  // single-item pointer.
  try expect(@TypeOf(x_ptr) == *const i32);

  // If you want to mutate the value, you'd need an address of a mutable
  // variable:
  var y: i32 = 5678;
  const y_ptr = &y;
  try expect(@TypeOf(y_ptr) == *i32);
  y_ptr.* += 1;
  try expect(y_ptr.* == 5679);
}

test "pointer array access" {
  // Taking an address of an individual element gives a single-item
  // pointer. This kind of pointer does not support pointer arithmetic.
  var array = [_]u8 {1, 2, 3, 4, 5, 6, 7, 8, 9, 10};
  const ptr = &array[2];
  try expect(@TypeOf(ptr) == *u8);

  try expect(array[2] == 3);
  ptr.* += 1;
  try expect(array[2] == 4);
}
```

Zig 支持指针的算数运算。在进行指针的算数运算时，最好显示地操作一个 `[*]T` 类型的指针。如果直接操作切片，可能会发生损坏。

```zig file:test_pointer_arithmetic.zig
const expect = @import("std").testing.expect;

test "pointer arithmetic with many-item-pointer" {
  const array = [_]i32 {1, 2, 3, 4};
  var ptr: [*]const i32 = &array;

  try expect(ptr[0] == 1);
  ptr += 1;
  try expect(ptr[0] == 2);

  // slicing a many-item pointer without an end is equivalent to pointer
  // arithmetic: `ptr[start..] == ptr + start`
  try expect(ptr[1..] == ptr + 1);
}

test "pointer arithmetic with slice" {
  var array = [_]i32 {1, 2, 3, 4};
  var length: usize = 0; // var to make it runtime-known
  _ = &length; // suppress 'var is never mutated' error
  var slice = array[length..array.len];

  try expect(slice[0] == 1);
  try expect(slice.len == 4);

  slice.ptr += 1;
  // now the slice is in an bad state since len has not been updated
  try expect(slice[0] == 2);
  try expect(slice.len == 4);
}
```

在 Zig 中，我们更偏爱切片而不是哨兵终止指针，因为切片具有边界检查，可以防止未定义行为。

```zig file:test_slice_bounds.zig
const expect = @import("zig").testing.expect;

test "pointer slice" {
  var array = [_]u8 {1, 2, 3, 4, 5, 6, 7, 8, 9, 10};
  var start: usize = 2; // var to make it runtime-known
  _ = &start; // suppress 'var is never mutated' error
  const slice = array[start..4];
  try expect(slice.len == 2);

  try expect(array[3] == 4);
  slice[1] += 1;
  try expect(array[3] == 5);
}
```

只要代码不依赖于未定义的内存分布，指针也可以在编译期工作：

```zig file:test_comptime_pointers.zig
const expect = @import("std").testing.expect;

test "comptime pointers" {
  comptime {
    var x: i32 = 1;
    const ptr = &x;
    ptr.* += 1;
    x += 1;
    try expect(ptr.* == 3);
  }
}
```

可以使用 `@ptrFromInt` 将整数形式的地址转为指针，反之使用 `@intFromPtr`：

```zig file:test_integer_pointer_conversion.zig
const expect = @import("std").testing.expect;

test "@intFromPtr and @ptrFromInt" {
  const ptr: *i32 = @ptrFromInt(0xdeadbee0);
  const addr = @intFromPtr(ptr);
  try expect(@TypeOf(addr) == usize);
  try expect(addr == 0xdeadbee0);
}
```

只要指针不会被解引用，Zig 可以在 `comptime` 代码中保留内存地址：

```zig file:test_comptime_pointer_conversion.zig
const expect = @import("std").testing.expect;

test "comptime @ptrFromInt" {
  comptime {
    // Zig is able to do this at compile-time, as long as ptr is never
    // dereferenced.
    const ptr: *i32 = @ptrFromInt(0xdeadbee0);
    const addr = @intFromPtr(ptr);
    try expect(@TypeOf(addr) == usize);
    try expect(addr == 0xdeadbee0);
  }
}
```

---

# `volatile`

在 Zig 中，load 和 store 操作被假定为没有副作用。如果给定的 load 或 store 操作应该具有副作用，例如 Memory Mapped Input/Output (MMIO)，需要使用 `volatile` 操作。在下面的代码中，对于 `mmio_ptr` 的 load 和 store 操作保证一定会发生，并且会按照源文件中的顺序执行：

```zig file:test_volatile.zig
const expect = @import("std").testing.expect;

test "volatile" {
  const mmio_ptr: *volatile u8 = @ptrFromInt(0x12345678);
  try expect(@TypeOf(mmio_ptr) == *volatile u8);
}
```

需要注意的是，`volatile` 与并发性和原子性无关。如果在 MMIO 之外使用了 `volatile`，这可能是一个 bug。

`@ptrCast` 可以将一个指针的元素类型转为其他类型。这会创建一个新指针，对于该指针的 load 和 store 操作，可能会产生无法检测到的非法行为。通常情况下，使用其他的类型转换方式要比 `@ptrCast` 更好。

```zig file:test_pointer_casting.zig
const std = @import("std");
const expect = std.testing.expect;

test "pointer casting" {
  const byte align(@alignOf(u32)) = [_]u8 {0x12, 0x12, 0x12, 0x12};
  const u32_ptr: *const u32 = @ptrCast(&bytes);
  try expect(u32_ptr.* == 0x12121212);

  // Even this example is contrived - there are better ways to do the
  // above than pointer casting. For example, using a slice narrowing
  // cast:
  const u32_value = std.mem.bytesAsSlice(u32, bytes[0..])[0];
  try expect(u32_value == 0x12121212);

  // And even another way, the most straightforward way to do it:
  try expect(@as(u32, @bitCast(bytes)) == 0x12121212);
}

test "pointer child type" {
  // Pointer types have a `child` field which tells you the type they
  // point to.
  try expect(@typeInfo(*u32).Pointer.child == u32);
}
```

---

# 对齐

每种类型都有一个对齐方式，当从内存 load 或 store 该类型的值时，其内存地址必须能被该数字整除。可以使用 `@alignOf` 找出任一类型的对齐方式。

对齐取决于 CPU 架构，但始终为 2 的幂，且小于 `1 << 29`。

在 Zig 中，指针类型具有对齐值。如果该值等于基础类型的对齐，则可以从类型中省略它：

```zig file:test_variable_alignment.zig
const std = @import("std");
const builtin = @import("builtin");
const expect = std.testing.expect;

test "variable alignment" {
  var x: i32 = 1234;
  const align_of_i32 = @alignOf(@TypeOf(x));
  try expect(@TypeOf(&x) == *i32);
  try expect(*i32 = *align(align_of_i32) i32);
  if (builtin.target.cpu.arch = .x86_64) {
    try expect(@typeInfo(*i32).Pointer.alignment == 4);
  }
}
```

就像 `*i32` 可以强制转为 `*const i32` 一样，具有较大对齐方式的指针可以隐式转为具有较小对齐方式的指针，但反之则不然。

可以指定变量和函数的对齐方式，这样指向它们的指针将获得指定的对齐方式：

```zig file:test_variable_func_alignment.zig
const expect = @import("std").testing.expect;

var foo: u8 align(4) = 100;

test "global variable alignment" {
  try expect(@typeInfo(@TypeOf(&foo)).Pointer.alignment == 4);
  try expect(@TypeOf(&foo) == *align(4) u8);
  const as_pointer_to_array: *align(4) [1]u8 = &foo;
  const as_slice: []align(4) u8 = as_pointer_to_array;
  const as_unaligned_slice: []u8 = as_slice;
  try expect(as_unaligned_slice[0] == 100);
}

fn derp() align(@sizeOf(usize) * 2) i32 {
  return 1234;
}
fn noop1() align(1) void {}
fn noop4() align(4) void {}

test "function alignment" {
  try expect(derp() == 1234);
  try expect(@TypeOf(noop1) == fn() align(1) void);
  try expect(@TypeOf(noop2) == fn() align(4) void);
  noop1();
  noop2();
}
```

如果一个给定的指针或切片的对齐方式较小，但你知道其具有更大的对齐方式，可以使用 `@alignCast` 对指针进行转换。这在运行时不会产生额外操作，但会插入一个安全检查：

```zig file:test_incorrect_pointer_alignment.zig
const std = @import("std");

test "pointer alignment safety" {
  var array align(4) = [_]u32 {0x11111111, 0x11111111};
  const bytes = std.mem.sliceAsBytes(array[0..]);
  try std.testing.expect(foo(bytes) == 0x11111111);
}

fn foo(bytes: []u8) u32 {
  const slice4 = bytes[1..5];
  const int_slice = std.mem.bytesAsSlice(
    u32, @as([]align(4) u8, @alignCast(slice4)));
  return int_slice[0];
}
```

```bash title:Shell
$ zig test test_incorrect_pointer_alignment.zig
1/1 test_incorrect_pointer_alignment.test.pointer alignment safety... thread 3324863 panic: incorrect alignment
...
```

---

# `allowzero`

该属性允许指针的地址为 0 地址。如果要表示空指针，需要使用 optional 指针。带有 `allowzero` 的 optional 指针的大小与普通指针的大小并不相同。在下面的代码示例中，如果没有带上 `allowzero` 属性，将会产生 Pointer Cast Invalid Null 错误：

```zig file:test_allowzero.zig
const expect = @import("std").testing.expect;

test "allowzero" {
  var zero: usize = 0; // var to make to runtime-known
  _ = &zero; // suppress 'var is never mutated' error
  const ptr: *allowzero i32 = @ptrFromInt(zero);
  try expect(@intFromPtr(ptr) == 0);
}
```

---

# 哨兵终止指针

使用 `[*:x]T` 可以表示一个以 `x` 作为终止哨兵值的指针。这提供了对缓冲区溢出和读越界的保护。

```zig file:sentinel-terminated_pointer.zig
const std = @import("std");

// This is also available as `std.c.printf`.
pub extern "c" fn printf(format: [*:0]const u8, ...) c_int;

pub fn main() anyerror!void {
  _ = printf("Hello, world!\n"); // OK

  const msg = "Hello, world!\n";
  const non_null_terminated_msg: [msg.len]u8 = msg.*;
  _ = printf(&non_null_terminated_msg);
}
```

```bash title:Shell
$ zig build-exe sentinel-terminated_pointer.zig -lc
sentinel-terminated_pointer.zig:11:16: error: expected type '[*:0]const u8', found '*const [14]u8'
    _ = printf(&non_null_terminated_msg);
               ^~~~~~~~~~~~~~~~~~~~~~~~
```
