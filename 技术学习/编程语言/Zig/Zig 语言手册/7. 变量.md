
---

变量是用于存储数据的内存区域。

声明变量时通常最好使用 `const` 而不是 `var`，这可以减少人类和计算机的心智负担，并创造更多的优化机会。

`extern` 关键字和 `@extern` 内建函数可用于链接从别的目标文件中导出的变量，`export` 关键字和 `@export` 内建函数可以将变量在链接时导出供其他目标文件使用。在这两种情况下，变量的类型必须与 C ABI 兼容。

---

# 标志符

变量标志符不允许遮蔽（shadow）外部作用域的标志符。

标志符必须以字母或下划线开头，后面可以跟任意数量的字母数字或下划线。不得与关键字重名。

如果需要的名称不符合这些要求（例如与外部库链接），则需要使用 `@""` 语法。

```zig file:identifiers.zig
const @"identifier with space in it" = 0xff;
const @"1SmallStep4Man" = 112358;

const c = @import("std").c;
pub extern "c" fn @"error"() void;
pub extern "c" fn @"fstat$INODE64"(fd: c.fd_t, buf: *c.Stat) c_int;

const Color = enum {
  red,
  @"really red",
};
const color: Color = .@"really red";
```

---

# 容器级变量

Zig 中的容器是指任何可以充当命名空间的语法结构，用于保存变量和函数声明。容器也是可以被实例化的类型定义。`struct`、`enum`、`union`、`opaque`，甚至 Zig 源文件本身都是容器。

尽管容器（Zig 源文件除外）使用大括号来包围其定义，但不应该将它们与块或函数混淆，容器不能包含任何语句。

容器级变量就是定义在容器中的变量。容器级变量具有：

- 静态生命周期（static lifetime）：生命周期与容器相同，被所有容器的实例共享。
- 顺序独立性（order-independent）：声明顺序对其行为没有影响。无论它们在容器内部的声明顺序如何，都不会影响它们的初始化或访问顺序。
- 惰性分析（lazily analyzed）：编译器只有在变量被实际使用时才会对其进行分析，而不会提前分析所有变量。

容器级变量的初始化值在声明时会被隐式地标记为 `comptime`。如果容器级变量被 `const` 修饰，那么该值为编译时已知的，否则就为运行时已知的。

```zig file:test_namespaced_container_level_varibal.zig
const std = @import("std");
const expect = std.testing.expect;

test "namespaced container level variable" {
  try expect(foo() == 1235);
  try expect(foo() == 1236);
}

const S = struct {
  var x: i32 = 1234;
};

fn foo() i32 {
  S.x += 1;
  return S.x;
}
```

---

# 静态局部变量

通过在函数内部使用容器级变量，还可以使局部变量具有静态声明周期：

```zig file:test_static_local_variable.zig
const std = @import("std");
const expect = std.testing.expect;

test "static local variable" {
  try expect(foo() == 1235);
  try expect(foo() == 1236);
}

fn foo() i32 {
  const S = struct {
    var x: i32 = 1234;
  };
  S.x += 1;
  return S.x;
}
```

---

# 线程局部变量

可以使用 `threadlocal` 关键字将变量指定为线程局部变量，使得每个线程都可以使用该变量的单独实例：

```zig file:test_thread_local_variables.zig
const std = @import("std");
const assert = std.debug.assert;

threadlocal var x: i32 = 1234;

test "thread local storage" {
  const thread1 = try std.Thread.spawn(.{}, testTls, .{});
  const thread2 = try std.Thread.spawn(.{}, testTls, .{});
  testTls();
  thread1.join();
  thread2.join();
}

fn testTls() void {
  assert(x == 1234);
  x += 1;
  assert(x == 1235);
}
```

对于单线程的构建来说，所有线程局部变量都会被视为常规的容器极变量。

---

# 局部变量

局部变量出现在函数、`comptime` 块和 `@cImport` 块中。

当局部变量被 `const` 关键字修饰时，说明该变量的值在初始化后不会发生任何改变。如果 `const` 变量的初始化值是编译期已知的，那么该变量也是编译期已知的。

局部变量可以被 `comptime` 关键字修饰。这说明该变量的值是编译期已知的，并且该变量的所有读写操作都在程序的语义分析期间发生，而不是在运行时发生。在 `comptime` 表达式中声明的所有变量都是隐式的 `comptime` 变量。

```zig file:test_comptime_variables.zig
const std = @import("std");
const expect = std.testing.expect;

test "comptime vars" {
  var x: i32 = 1;
  comptime var y: i32 = 1;

  x += 1;
  y += 1;

  try expect(x == 2);
  try expect(y == 2);

  if (y != 2) {
    // This compile error never triggers because y is a comptime
    // variable, and so `y != 2` is a comptime value, and this if is
    // statically evaluated.
    @compileError("wrong y value");
  }
}
```
