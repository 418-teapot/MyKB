
---

块用来限制变量声明的范围：

```zig file:test_blocks.zig
test "access variable after block scope" {
  {
    var x: i32 = 1;
    _ = &x;
  }
  x += 1;
}
```

```bash
$ zig test test_blocks.zig
test_blocks.zig:6:5: error: use of undeclared identifier 'x'
    x += 1;
    ^
```

块也是表达式。当被标记后，可以使用 `break` 从块中返回一个值：

```zig file:test_labeled_break.zig
const std = @import("std");
const expect = std.testing.expect;

test "labeled break from labeled block expression" {
  var y: i32 = 123;

  const x = blk: {
    y += 1;
    break :blk y;
  };
  try expect(x == 124);
  try expect(y == 124);
}
```

其中，`blk` 可以是任意名称。

---

# 遮蔽

使用相同名称的标志符来“隐藏”其他标志符是不允许的：

```zig file:test_shadowing.zig
const pi = 3.14;

test "inside test block" {
  // Let's even go inside another block.
  {
    var pi: i32 = 1234;
  }
}
```

```bash
$ zig test test_shadowing.zig
test_shadowing.zig:6:13: error: local variable shadows declaration of 'pi'
        var pi: i32 = 1234;
            ^~
test_shadowing.zig:1:1: note: declared here
const pi = 3.14;
^~~~~~~~~~~~~~~
```

标志符在其定义的范围内始终表示相同的含义。但是，如果作用域不同，可以使用相同的标志符表示不同的含义：

```zig file:test_scopes.zig
test "sepatate scopes" {
  {
    const pi = 3.14;
    _ = pi;
  }
  {
    var pi: bool = true;
    _ = &pi;
  }
}
```

---

# 空块

一个空块等价于 `void {}`：

```zig file:test_empty_block.zig
const std = @import("std");
const expect = std.testing.expect;

test {
  const a = {};
  const b = void {};
  try expect(@TypeOf(a) == void);
  try expect(@TypeOf(b) == void);
  try expect(a == b);
}
```
