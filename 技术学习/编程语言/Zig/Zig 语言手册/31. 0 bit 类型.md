
---

对于某些类型，使用 `@sizeOf` 得到的结果是 0：

- void
- `u0` 和 `i0` 整型；
- 长度为 0，或者元素类型为 0 bit 类型的数组和向量；
- 只有一个标签的枚举；
- 所有字段均为 0 bit 类型的结构体；
- 只有一个字段，且其为 0 bit 类型的联合。

这些类型只会有一个可能的值，因此只需要 0 bit 就可以表示。使用这些类型的代码不会包含在最终生成的代码中：

```zig file:zero_bit_types.zig
export fn entry() void {
  var x: void = {};
  var y: void = {};
  x = y;
  y = x;
}
```

在最终生成的机器码中，没有任何代码生成在 `entry` 中，即使是在 Debug 模式下。例如 x86_64 汇编：

```nasm file:zero_bit_types.s
0000000000000010 <entry>:
  10:	55                   	push   %rbp
  11:	48 89 e5             	mov    %rsp,%rbp
  14:	5d                   	pop    %rbp
  15:	c3                   	retq 
```

---

# `void`

`void` 对于实例化泛型类型很有用。例如，给定一个 `Map(Key, Value)`，可以为 `Value` 传递 `void`，这样就可以实例化其为 `Set`：

```zig file:test_void_in_hashmap.zig
const std = @import("std");
const expect = std.testing.expect;

test "turn HashMap into a set with void" {
  var map = std.AutoHashMap(i32, void).init(std.testing.allocator);
  defer map.deinit();

  try map.put(1, {});
  try map.put(2, {});

  try expect(map.contains(2));
  try expect(!map.contains(3));

  _ = map.remove(2);
  try expect(!map.contains(2));
}
```

需要注意的是，这与使用 dummy 值作为哈希表的 value 不同。使用 `void` 作为 value 时，哈希表的每一行记录都没有 value 字段，可以使得哈希表占用更少的空间。此外，所有涉及到对 value 的加载和存储操作都会被删除。

`void` 与 `anyopaque` 不同，`void` 的大小是 0 字节，而 `anyopaque` 的大小未知且不为 0。

`void` 类型的表达式是唯一其值可以被忽略的表达式，其他类型的表达式的值被忽略时会触发编译错误：

```zig file:test_expression_ignored.zig
test "ignoring expression value" {
  foo();
}

fn foo() i32 {
  return 1234;
}
```

```bash
$ zig test test_expression_ignored.zig
test_expression_ignored.zig:2:8: error: value of type 'i32' ignored
    foo();
    ~~~^~
test_expression_ignored.zig:2:8: note: all non-void values must be used
test_expression_ignored.zig:2:8: note: to discard the value, assign it to '_'
```

如果表达式的类型是 `void`，那么就不会触发编译错误；对于上述情况，可以通过将表达式的结果赋值给 `_` 来显示忽略它们：

```zig file:test_void_ignored.zig
test "void is ignored" {
  returnsVoid();
}

test "explicitly ignoring expression value" {
  _ = foo();
}

fn returnsVoid() void {}

fn foo() i32 {
  return 1234;
}
```
