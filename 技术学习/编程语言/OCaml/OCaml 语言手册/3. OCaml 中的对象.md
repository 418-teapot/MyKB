
---

需要注意的是，OCaml 中对象、类、类型之间的关系与 Java 和 C++ 等面向对象语言中的关系不同。与这些语言相比，Ocaml 中使用面向对象功能的频率要低得多。OCaml 通常有更合适的替代方案，例如模块和函子。事实上，许多 OCaml 程序根本不使用对象。

---

# 类与对象

下面的类 `point` 定义了一个实例变量 `x` 和两个方法 `get_x` 和 `move`。这个实例变量的初始值为 `0`。变量 `x` 被声明为可变的，因此 `move` 方法可以改变它的值。

```ocaml
# class point =
    object
      val mutable x = 0
      method get_x = x
      method move d = x <- x + d
    end;;
```

```
class point :
  object val mutable x : int method get_x : int method move : int -> unit end
```

我们现在创建一个点 `p`，即 `point` 类的实例。

```ocaml
# let p = new point;;
```

```
val p : point = <obj>
```

需要注意的是 `p` 的类型是 `point`。这是由上面的类定义自动定义的缩写，它代表对象类型 `<get_x : int; move : int -> unit>`（列出类 `point` 的方法及其类型）。

我们可以调用 `p` 中的一些方法：

```ocaml
# p#get_x;;
```

```
- : int = 0
```

```ocaml
# p#move 3;;
```

```
- : unit = ()
```

```ocaml
# p#get_x;;
```

```
- : int = 3
```

类体的执行仅在对象创建时进行。因此，在下面的示例中，实例变量 `x` 对于两个不同的对象被初始化为不同的值：

```ocaml
# let x0 = ref 0;;
```

```
val x0 : int ref = {contents = 0}
```

```ocaml
# class point =
    object
      val mutable x = incr x0; !x0
      method get_x = x
      method move d = x <- x + d
    end;;
```

```
class point :
  object val mutable x : int method get_x : int method move : int -> unit end
```

```ocaml
# new point#get_x;;
```

```
- : int = 1
```

```ocaml
new point#get_x;;
- : int = 2
```

类 `point` 也可以通过 `x` 的初始值进行抽象。

```ocaml
# class point = fun x_init ->
    object
      val mutable x = x_init
      method get_x = x
      method move d = x <- x + d
    end;;
```

```
class point :
  int ->
  object val mutable x : int method get_x : int method move : int -> unit end
```

与函数定义一样，上面的定义可以缩写为：

```ocaml
# class point x_init =
    object
      val mutable x = x_init
      method get_x = x
      method move d = x <- x + d
    end;;
```

```
class point :
  int ->
  object val mutable x : int method get_x : int method move : int -> unit end
```

类 `point` 的实例现在是一个需要初始化参数来创建对象的函数：

```ocaml
# new point;;
```

```
- : int -> point = <fun>
```

```ocaml
# let p = new point 7;;
```

```
val p : point = <obj>
```

参数 `x_init` 在定义的整个类体中都是可见的，包括方法。例如，下面的 `get_offset` 方法会返回对象相对于其初始位置的偏移。

```ocaml
# class point x_init =
    object
      val mutable x = x_init
      method get_x = x
      method get_offset = x - x_init
      method move d = x <- x + d
    end;;
```

```
class point :
  int ->
  object
    val mutable x : int
    method get_x : int
    method get_offset : int
    method move : int -> unit
  end
```

在定义类体之前可以对表达式进行求值和绑定。这对于强制不变量来说十分有用。例如，点可以自动调整到网格上最近的点，如下所示：

```ocaml
# class adjusted_point x_init =
    let origin = (x_init / 10) * 10 in
      object
        val mutable x = origin
        method get_x = x
        method get_offset = x - origin
        method move d = x <- x + d
      end;;
```

```
class adjusted_point :
  int ->
  object
    val mutable x : int
    method get_x : int
    method get_offset : int
    method move : int -> unit
  end
```

（如果 `x_init` 坐标不在网格上，也可以触发异常。）事实上，通过使用 `origin` 的值来初始化 `point` 类的定义，也可以得到相同的效果。

```ocaml
# class adjusted_point x_init = point ((x_init / 10) * 10);;
```

```
class adjusted_point : int -> point
```

另一种方法是在特殊分配函数中定义：

```ocaml
# let new_adjusted_point x_init = new point ((x_init / 10) * 10);;
```

```
val new_adjusted_point : int -> point = <fun>
```

然而，前一种模式通常会更合适，因为用于调整的代码是类定义的一部分，并且将被继承。

这种能力提供了类构造器，类似于其他语言中的实现方式。可以通过定义多个构造器来创建同一个类的对象，但可以具有不同的初始化模式；另一种方法是使用初始化器，如下面章节所述。

---

# 立即对象

还有另一种更直接的方式来创建对象：不通过类来创建它。

语法与类表达式完全一致，但结果是单个对象而不是类。本节其余部分描述的所有构造也适用于立即对象。

```ocaml
# let p =
    object
      val mutable x = 0
      method get_x = x
      method move d = x <- x + d
    end;;
```

```
val p : < get_x : int; move : int -> unit > = <obj>
```

```ocaml
# p#get_x;;
```

```
- : int = 0
```

```ocaml
# p#move 3;;
```

```
- : unit = ()
```

```ocaml
# p#get_x;;
```

```
- : int = 3
```

与类不同，类不能在表达式内部进行定义，而立即对象可以出现在任何地方，可以使用它们环境中的变量。

```ocaml
# let minmax x y =
    if x < y then object method min = x method max = y end
    else object method min = y method max = x end;;
```

```
val minmax = 'a -> 'a -> < max : 'a; min : 'a > = <fun>
```

与类相比，立即对象有两个缺点：它们的类型没有缩写，并且不能继承它们。但在某些情况下，这两个缺点可能是优势。

---

# 引用自身

方法或初始化器可以调用自身（即当前对象）上的方法。为此，`self` 必须显示绑定，这里绑定到变量 `s` 上（`s` 可以是任何标识符，尽管我们经常选择名称 `self`）。

```ocaml
# class printable_point x_init =
    object (s)
      val mutable x = x_init
      method get_x = x
      method move d = x <- x + d
      method print = print_int s#get_x
    end;;
```

```
class printable_point :
  int ->
  object
    val mutable x : int
    method get_x : int
    method move : int -> unit
    method print : unit
  end
```

```ocaml
# let p = new printable_point 7;;
```

```
val p : printable_point = <obj>
```

```ocaml
# p#print;;
7- : unit = ()
```

动态地，变量 `s` 在方法调用时进行绑定。特别的是，当类 `printable_point` 被继承时，变量 `s` 将被正确地绑定到子类的对象上。

`self` 的一个常见问题是，由于它的类型可能会在子类中扩展，因此无法提前固定它。这是一个简单的例子。

```ocaml
# let ints = ref [];;
```

```
val ints : '_weak1 list ref = {contents = []}
```

```ocaml
# class my_int =
    object (self)
      method n = 1
      method register = ints := self :: !ints
    end;;
```

```
Error: This expression has type < n : int; register : 'a; .. >
       but an expression was expected of type 'weak1
       Self type cannot escape its class
```

重要的是报错信息的最后一行：将 `self` 放入外部引用将使其无法通过继承进行扩展。在后续章节中会介绍解决方法。但需要注意的是，由于立即对象是不可扩展的，所有不会出现此问题。

```ocaml
# let my_int =
    object (self)
      method n = 1
      method register = ints := self :: !ints
    end;;
```

```
val my_int : < n : int; register : unit > = <obj>
```

---

# 初始化器

类定义的 let 绑定是在对象创建之前就会执行的。也可以在对象创建之后立即执行表达式。此类代码应编写在匿名的隐藏方法（称为初始化器）中。因此，它可以访问 `self` 和实例变量。

```ocaml
# class printable_point x_init =
    let origin = (x_init / 10) * 10 in
    object (self)
      val mutable x = origin
      method get_x = x
      method move d = x <- x + d
      method print = print_int self#get_x
      initializer print_string "new point at "; self#print; print_newline ()
    end;;
```

```
class printable_point :
  int ->
  object
    val mutable x : int
    method get_x : int
    method move : int -> unit
    method print : unit
  end
```

```ocaml
# let p = new printable_point 17;;
```

```
new point at 10
val p : printabel_point = <obj>
```

初始化器不能被重写。相反，所有初始化器都是按照顺序执行的。初始化器对于强制不变量特别有用。

---

# 虚方法

使用关键字 `virtual` 可以声明一个方法而不实际定义它。该方法稍后会在子类中提供。包含虚方法的类必须标记为 `virtual`，并且不能被实例化（即，不能创建该类的任何对象）。

```ocaml
# class virtual abstract_point x_init =
    object (self)
      method virtual get_x : int
      method get_offset = self#get_x - x_init
      method virtual move : int -> unit
    end;;
```

```
class virtual abstract_point :
  int ->
  object
    method get_offset : int
    method virtual get_x : int
    method virtual move : int -> unit
  end
```

```ocaml
# class point x_init =
    object
      inherit abstract_potint x_init
      val mutable x = x_init
      method get_x = x
      method move d = x <- x + d
    end;;
```

```
class point :
  int ->
  object
    val mutable x : int
    method get_offset : int
    method get_x : int
    method move : int -> unit
  end 
```

实例变量也可以声明为 `virtual`，其效果与方法相同。

```ocaml
# class virtual abstract_point2 =
    object
      val mutable virtual x : int
      method move d = x <- x + d
    end;;
```

```
class virtual abstract_point2 :
  object val mutable virtual x : int method move : int -> unit end
```

```ocaml
# class point2 x_init =
    object
      inherit abstract_point2
      val mutable x = x_init
      method get_offset = x - x_init
    end;;
```

```
class point2 :
  int ->
  object
    val mutable x : int
    method get_offset : int
    method move : int -> unit
  end
```

---

# 私有方法

私有方法是不会出现在对象接口中的方法。它们只能从同一对象的其他方法中调用。

```ocaml
# class restricted_point x_init =
    object (self)
      val mutable x = x_init
      method get_x = x
      method private move d = x <- x + d
      method bump = self#move 1
    end;;
```

```
class restricted_point :
  int ->
  object
    val mutable x : int
    method bump : unit
    method get_x : int
    method private move : int -> unit
  end
```

```ocaml
# let p = new restricted_point 0;;
```

```
val p : restricted_point = <obj>
```

```ocaml
# p#move 10;;
```

```
Error: This expression has type restricted_point
       It has no method move
```

```ocaml
# p#bump;;
```

```
- : unit = ()
```

需要注意的是，这与 Java 或 C++ 中的私有方法和保护方法不同，后者可以从同一类的其他对象调用。这是 OCaml 中类型和类之间独立性的直接结果：两个不相关的类可能会产生相同类型的对象，并且在类型级别上无法确保对象来自确定的类。

私有方法是可继承的（默认情况下它们在子类中可见），除非通过签名匹配来隐藏。

私有方法可以在子类中公有。

```ocaml
# class point_again x =
    object (self)
      inherit restricted_point x
      method virtual move : _
    end;;
```

```
class point_again :
  int ->
  object
    val mutable x : int
    method bump : unit
    method get_x : int
    method move : int -> unit
  end
```

这里的 `virtual` 注解只是用来标注一个方法，而不提供它的定义。由于我们没有添加 `private` 注解，这使得该方法成为公有方法，并保留了原始定义。

另一种定义是

```ocaml
# class point_again x =
    object ( self : < move : _; ..> )
      inherit restricted_point x
    end;;
```

```
class point_again :
  int ->
  object
    val mutable x : int
    method bump : unit
    method get_x : int
    method move : int -> unit
  end
```

在这种定义中，对于 `self` 类型的约束是具有一个公有的 `move` 方法，这足以覆盖掉 `private`。

人们可能认为私有方法应该在子类中保持私有性。但是，由于该方法在子类中可见，就始终可以定义一个同名的方法来运行其代码：

```ocaml
# class point_again x =
    object
      inherit restricted_point x as super
      method move = super#move
    end;;
```

```
class point_again :
  int ->
  object
    val mutable x : int
    method bump : unit
    method get_x : int
    method move : int -> unit
  end
```

当然，私有方法也可以是 `virtual` 的。关键字必须按以下顺序出现：`method private virtual`。

---

# 类接口

类接口是从类定义中推断出来的。它们也可以直接定义并用于限制类的类型。与类声明一样，它们也定义了新的类型缩写。

```ocaml
# class type restricted_point_type =
    object
      method get_x : int
      method bump : unit
    end;;
```

```
class type restricted_point_type =
  object method bump : unit method get_x : int end
```

```ocaml
# fun (x : restricted_point_type) -> x;;
```

```
- : restricted_point_type -> restricted_point_type = <fun>
```

除了程序文档之外，还可以使用类接口来约束类的类型。实际的实例变量和私有方法都可以通过类类型约束隐藏。然而，公有方法和虚方法则不能。

```ocaml
# class restricted_point' x = (restricted_point x : restricted_point_type);;
```

```
class restricted_point' : int -> restricted_point_type
```

或者，等价的：

```ocaml
# class restricted_point' = (restricted_point : int -> restricted_point_type);;
```

```
class restricted_point' : int -> restricted_point_type
```

类的接口也可以在模块签名中指定，并用于限制模块的推断签名。

```ocaml
# module type POINT = sig
    class restricted_point' : int ->
      object
        method get_x : int
        method bump : unit
      end
    end;;
```

```
module type POINT =
  sig
    class restricted_point' :
      int -> object method bump : unit method get_x : int end
    end
```

```ocaml
# module Point : POINT = struct
    class restricted_point' = restricted_point
  end;;
```

```
module Point : POINT
```
