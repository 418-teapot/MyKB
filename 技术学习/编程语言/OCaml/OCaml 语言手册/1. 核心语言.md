
---

# 基本

对于 OCaml 的概述，我们使用交互式系统，该系统通过命令行运行 `ocaml` 来启动。本教程以交互式系统会话的记录形式来呈现：以 `#` 开头的行代表用户输入；系统响应在下面，不以 `#` 开头。

在交互式系统下，用户输入以 `#` 开头 `;;` 结尾的 OCaml 短语，系统即时进行编译、执行、并打印结果。短语要么是简单的表达式，要么是 `let` 定义的标识符（值或函数）。

```ocaml
# 1 + 2 * 3;;
- : int = 7
```

```ocaml
# let pi = 4.0 .* atan 1.0;;
val pi : float = 3.14159265358979312
```

```ocaml
# let square x = x *. x;;
val square : float -> float = <fun>
```

```ocaml
# square (sin pi) +. square (cos pi);;
- : float = 1.
```

OCaml 系统会计算每个短语的值和类型。函数参数不需要显式地声明类型，系统会根据它们在函数中的用法推断出它们的类型。需要注意的是，整数和浮点数是不同的类型，具有不同的运算符：整数运算使用 `+` 和 `*`，浮点数运算使用 `+.` 和 `*.` 。

```ocaml
# 1.0 * 2;;
Error: This expression has type float but an expression was expected of type int
```

递归函数的定义是通过 `let rec` 绑定的：

```ocaml
# let rec fib b =
    if n < 2 then n else fib (n - 1) + fib (n - 2);;
val fib : int -> int = <fun>
```

```ocaml
# fib 10;;
- : int = 55
```

---

# 数据类型

除了整型和浮点型之外，OCaml 还提供了常用的基本数据类型：

- bool 型

```ocaml
# (1 < 2) = false;;
- : bool = false
```

```ocaml
# let one = if true then 1 else 2;;
val one : int = 1
```

- char 型

```ocaml
# 'a';;
- : char = 'a'
```

```ocaml
# int_of_char '\n';;
- : int = 10
```

- 不可变字符串

```ocaml
# "Hello" ^ " " ^ "world";;
- : string = "Hello world"
```

```ocaml
# {|This is a quoted string, here, neither \ nor " are special characters|};;
- : string =
"This is a quoted string, here, neither \\ nor \" are special characters"
```

```ocaml
# {|"\\"|} = "\"\\\\\""
- : bool = true
```

```ocaml
# {delimiter|the end of this|}quoted string is here|delimter}
  = "the end of this|}quoted string is here";;
- : bool = true
```

预定义数据结构包括元组、数组和列表。还有一些通用机制可以定义自己的数据结构，例如记录和变体。现在，我们专注于列表。列表要么通过分号分隔元素的括号列表给出，要么通过使用 `::` 运算符在空列表 `[]` 前添加元素构建。

```ocaml
# let l = ["is"; "a"; "tale"; "told"; "etc."];;
val l : string list = ["is"; "a"; "tale"; "told"; "etc."]
```

```ocaml
# "Life" :: l;
- : string list = ["Life"; "is"; "a"; "tale"; "told"; "etc."]
```

与其他数据结构一样，列表不需要显式地从内存中分配和释放：所有内存管理在 OCaml 中都是完全自动的。同样，没有显式的指针处理：OCaml 编译器在必要时会静默地引入指针。

与大多数数据结构一样，列表的检查和析构是通过模式匹配执行的。列表模式与列表表达式具有完全相同的形式，其中标识符表示列表中未指定的部分。作为示例，下面是列表上的插入排序：

```ocaml
# let rec sort lst =
    match lst with
      [] -> []
    | head :: tail -> insert head (sort tail)
  and insert elt lst =
    match lst with
      [] -> [elt]
    | head :: tail -> if elt <= head then elt :: lst
                                     else head :: insert elt tail
  ;;
val sort : 'a list -> 'a list = <fun>
val insert : 'a -> 'a list -> 'a list = <fun>
```

```ocaml
# sort l;;
- : string list = ["a"; "etc."; "is"; "tale"; "told"]
```

OCaml 推断的 `sort` 的类型：`'a list -> 'a list`，意味着 `sort` 可以应用于任何类型的列表，并返回相同类型的列表。类型 `'a` 是一个类型变量，代表任何给定的类型。`sort` 可以应用于任何类型的列表的原因是，OCaml 中的比较操作（`=`, `<=` 等）是多态的：它们可以在任意两个相同类型的值之间操作。这使得 `sort` 对于所有列表类型都具有多态性。

```ocaml
# sort [6; 2; 5; 3];;
- : int list = [2; 3; 5; 6]
```

```ocaml
# sort [3.14; 2.718];;
- : float list = [2.718; 3.14]
```

上面的 `sort` 函数不会修改其输入的列表：它会构建并返回升序的、与输入列表具有相同元素的新列表。实际上，OCaml 中无法在构建列表中后原位修改，因此我们可以说，列表是不可变数据结构。大多数 OCaml 数据结构都是不可变的，但是少数（尤其是数组）是可变的，这意味着它们随时可能被原位修改。

具有多个参数的函数类型在 OCaml 中表示为： `arg1_type -> arg2_type -> ... -> return_type`。例如，为 `insert` 推断的类型 `'a -> 'a list -> 'a list` 意味着 `insert` 接收两个参数：任意类型 `'a` 的元素和包含相同类型 `'a` 的元素列表，并返回相同类型的列表。

---

# 函数作为值

OCaml 是一门函数式语言：支持完整数学意义上的函数，并且可以像任何其他数据一样可以自由传递。例如，下面是一个 `deriv` 函数，它接收任何浮点函数作为参数并返回其导数函数的近似值：

```ocaml
# let deriv f dx = fun x -> (f (x +. dx) -. f x) /. dx;
val deriv : (float -> float) -> float -> float -> float = <fun>
```

```ocaml
# let sin' = deriv sin 1e-6;;
val sin' : float -> float = <fun>
```

```ocaml
# sin' pi;;
- : float = -1.00000000013961143
```

甚至函数组合也是可以定义的：

```ocaml
# let compose f g = fun x -> f (g x);;
val compose : ('a -> 'b) -> ('c -> 'a) -> 'c -> 'b = <fun>
```

```ocaml
# let cos2 = compose square cos;;
val cos2 : float -> float = <fun>
```

以其他函数作为参数的函数称为“泛函”或“高阶函数”。泛函在为数据结构提供迭代器会类似的通用操作特别有用。例如，OCaml 标准库提供了一个 `List.map` 函数，它将给定的函数应用于列表中的每个元素，并返回结果列表：

```ocaml
# List.map (fun n -> n * 2 + 1) [0; 1; 2; 3; 4];;
- : int list = [1; 3; 5; 7; 9]
```

该泛函以及许多其他列表和数组泛函因为很有用，所有被预定义出来，但它没有什么魔法：它可以轻松地如下定义出来。

```ocaml
# let rec map f l =
    match l with
      [] -> []
    | head :: tail -> f head :: map f tail;;
val map : ('a -> 'b) -> 'a list -> 'b list = <fun>
```

---

# 记录与变体

用户定义数据结构包括记录与变体。两者都是通过类型声明来定义的。在这里，我们声明一个类型来表示有理数。

```ocaml
# type ratio = {num: int; denom: int};;
type ration = { num : int; denom : int; }
```

```ocaml
# let add_ratio r1 r2 =
    {num = r1.num * r2.denom + r2.num * r1.denom;
     denom = r1.denom * r2.denom};;
val add_ration : ratio -> ratio -> ratio = <fun>
```

```ocaml
# add_ratio {num = 1; denom = 3} {num = 2; denom = 5};;
- : ratio = {num = 11; denom = 15}
```

记录的字段也可以通过模式匹配来访问：

```ocaml
# let integer_part r =
    match r with
      {num = num; denom = denom} -> num / denom;;
val integer_part : ratio -> int = <fun>
```

由于此模式匹配中只有一种情况，因此可以安全地直接扩展记录模式中的参数 `r`：

```ocaml
# let integer_part {num = num; denom = denom} = num / denom;;
val integer_part : ratio -> int = <fun>
```

不需要的字段可以省略：

```ocaml
# let get_denom {denom = denom} = denom;;
val get_denom : ratio -> int = <fun>
```

或者，可以在列表的结尾使用尾随通配符 `_` 来表明缺失的字段：

```ocaml
# let get_num {num = num; _} = num;;
val get_num : ratio -> int = <fun>
```

当 `=` 符号的两侧相同时，可以通过省略 `=field` 部分来避免重复的字段名称：

```ocaml
# let integer_part {num; denom} = num / demon;;
val integer_part : ratio -> int = <fun>
```

这种字段的简洁表示在构建记录时也适用：

```ocaml
let ratio num denom = {num; denom};;
val ratio : int -> int -> ratio = <fun>
```

可以一次更新记录中的多个字段：

```ocaml
# let integer_product integer ratio = {ratio with num = integer * ratio.num};;
val integer_product : int -> ratio -> ratio = <fun>
```

通过这个更新符号，记录中 `with` 右边的字段更新之后，左边的字段也会进行拷贝。

变体类型的声明列出了该类型值的所有可能的形式。每个 case 都由一个称为构造器的名称来标识，该名称既可以用于构造变体类型的值，又可以用于模式匹配中。构造器的名称必须以大写字母开头，以区别于变量名称（必须与小写字母开头）。例如，下面是一个执行混合算术运算（整数和浮点数）的变体类型：

```ocaml
# type number = Int of int | Float of float | Error;;
type number = Int of int | Float of float | Error
```

该声明表示 `number` 类型的值可以是整数、浮点数，也可以是常量 `Error` 表示无效运算（例如除以 0）。

枚举类型是变体类型的特例，它的所有替代项都是常量：

```ocaml
# type sign = Positive | Negative;;
type sign = Positive | Negative
```

```ocaml
# let sign_int n = if n >= 0 then Positive else Negative;;
val sign_int : int -> sign = <fun>
```

为了定义 `number` 类型的算数运算，我们可以对涉及的两个数使用模式匹配：

```ocaml
# let add_num n1 n2 =
    match (n1, n2) with
      (Int i1, Int i2) ->
        (* Check for overflow of integer addition *)
        if sign_int i1 = sign_int i2 && sign_int (i1 + i2) <> sign_int i1
        then Float(float i1 +. float i2)
        else Int(i1 + i2)
    | (Int i1, Float f2) -> Float(float i1 +. f2)
    | (Float f1, Int i2) -> Float(f1 +. float i2)
    | (Float f1, Float f2) -> Float(f1 +. f2)
    | (Error, _) -> Error
    | (_, Error) -> Error;;
val add_num : number -> number -> number = <fun>
```

```ocaml
# add_num (Int 123) (Float 3.14159);;
- : number = Float 126.14159
```

变体类型的另一个有趣的例子是内建的 `'a option` 类型，它表示一个 `'a` 类型的值或者不存在值：

```ocaml
# type 'a option = Some of 'a | None;;
type 'a option = Some of 'a | None
```

这个类型非常有用，其实是在定义某些情况下可能失败的函数时，例如：

```ocaml
# let safe_square_root x = if x >= 0. then Some(sqrt x) else None;;
val safe_square_root : float -> float option = <fun>
```

变体类型最常见的用法是描述递归数据结构。例如考虑二叉树的类型：

```ocaml
# type 'a btree = Empty | Node of 'a * 'a btree * 'a btree;;
type 'a btree = Empty | Node of 'a * 'a btree * 'a btree
```

该定义如下：包含类型 `'a` 值的二叉树要么为空，要么是包含一个类型 `'a` 值的结点和两个也包含类型 `'a` 值的子树，即两个 `'a btree`。

二叉树上的操作可以自然的表示为递归函数，遵循与类型定义本身相同的结构。例如，一下是在有序二叉树中执行查找和插入的函数（元素自左向右递增）：

```ocaml
# let rec member x btree 
    match btree with
      Empty -> false
    | Node(y, left, right) ->
      if x = y then true else
      if x < y then member x left else member x right;;
val member : 'a -> 'a btree -> bool = <fun>
```

```ocaml
# let rec insert x btree =
    match btree with
      Empty -> Node(x, Empty, Empty)
    | Node(y, left, right) ->
      if x <= y then Node(y, insert x left, right)
                else Node(y, left, insert x right);;
val insert : 'a -> 'a btree -> 'a btree = <fun>
```

## 记录和变体的歧义消除

精明的读者可能想知道当两个或多个记录字段或构造器具有相同名称时会发生什么。

```ocaml
# type first_record = {x: int; y: int; z: int}
  type middle_record = {x: int; z: int}
  type last_record = {x: int};;
```

```ocaml
# type first_variant = A | B | C
  type second_variant = A;;
```

答案时，当面对多个选项时，OCaml 会尝试使用本地可用的信息来消除各个字段和构造器之间的歧义。首先，如果记录或变体的类型已知，OCaml 可以明确得选择相应的字段或构造器。例如：

```ocaml
# let look_at_x_then_z (r: first_record) =
    let x = r.x in
    x + r.z;;
val look_at_x_the_z : first_record -> int = <fun>
```

```ocaml
# let permute (x: first_variant) = match x with
    | A -> (B: first_variant)
    | B -> A
    | C -> C;;
val permute : first_variant -> first_variant = <fun>
```

```ocaml
# type wrapped = First of first_record
  let f (First r) = r, r.x;;
type wrapped = First of first_record
val f : wrapped -> first_record * int = <fun>
```

在第一个例子中，`(r: first_record)` 是一个显式注解，告诉 OCaml `r` 的类型是 `first_record`。通过这个注解，OCaml 得知 `r.x` 指的是 `first_record` 类型的 `x` 字段。同样，第二个例子中的类型注解使 OCaml 清楚地知道构造器 `A`、`B` 和 `C` 来自 `first_variant` 类型。相反，在最后一个例子中，OCaml 自己推断出 `r` 的类型只能是 `first_record`，所以不需要显式的类型注解。

这些显式的类型注解实际上可以在任何地方使用。大多数时候它们不是必要的，但它们对于指导消除歧义、调试意外的类型错误或与后面章节中描述的 OCaml 的一些高级功能结合，是非常有用的。

其次，对于记录，OCaml 还可以查看表达式或模式中使用的整个字段集来推断正确的记录类型：

```ocaml
# let project_and_rotate {x; y; _} = {x = -y; y = x; z = 0};;
val project_and_rotate : first_record -> first_record = <fun>
```

由于字段 `x` 和 `y` 只会同时出现在 `first_record` 类型中，因此 OCaml 推断 `project_and_rotate` 的类型是 `first_record -> first_record`。

在最后的手段中，如果没有足够的信息来消除不同字段或构造器之间的歧义，OCaml 会在所有局部有效选择中选择最后定义的类型：

```ocaml
# let look_at_xz {x; z} = x;;
val look_at_xz : middle_record -> int = <fun>
```

这里，OCaml 推断出 `{x; z}` 类型的可能选择是 `first_record` 和 `middle_record`，因为类型 `last_record` 没有 `z` 字段。由于 `middle_record` 类型是最后定义的，因此 OCaml 选择了该类型。

需要注意的是，这个最后的消除歧义的手段是局部的：一旦 OCaml 选择了一种消除歧义的手段，它就会坚持这个选择，即使会导致类型错误：

```ocaml
# let look_at_x_then_y r =
    let x = r.x in (* Ocaml deduces [r: last_record] *)
    x + r.y;;
Error: This expression has type last_record
       There is no field y within type last_record
```

```ocaml
# let is_a_or_b x = match x with
    | A -> true (* Ocaml infers [x: last_variant] *)
    | B -> true;;
Error: This variant pattern is expected to have type last_variant
       There is no constructor B within type last_variant
```

此为，类型最后定义的位置是非常不稳定的，在添加或移动类型定义之后，或者在打开模块之后，这个位置都可能会发生改变。因此，显式地添加类型注解来指导消除歧义比依赖最后定义的类型会更加稳健。

---

# 命令式特性

尽管到目前位置的所有示例都是以纯粹的函数式风格写的，但 OCaml 还具有完整的命令式特性。这包括常见的 `while` 和 `for` 循环，以及数组等可变数据结构。数组是通过在 `[|` 和 `|]` 之间列出分号分隔的元素创建的，或者使用 `Array.make` 来分配空间和初始化，然后通过赋值来填充。例如，下面的函数按分量对两个向量（表示为浮点数组）进行求和：

```ocaml
# let add_vect v1 v2 =
    let len = min (Array.length v1) (Array.length v2) in
    let res = Array.make len 0.0 in
    for i = 0 to len - 1 do
      res.(i) <- v1.(i) +. v2.(i)
    done
    res;;
val add_vect : float array -> float array -> float array = <fun>
```

```ocaml
# add_vect [|1.0; 2.0|] [|3.0; 4.0|];;
- : float array = [|4.; 6.|]
```

记录的字段也可以通过赋值进行修改，前提是它们在记录类型的定义中声明为 `mutable` 的：

```ocaml
# type mutable_point = {mutable x: float; mutable y: float};;
type mutable_point = { mutable x : float; mutable y : float; }
```

```ocaml
# let translate p dx dy =
    p.x <- p.x +. dx; p.y <- p.y +. dy;;
val translate : mutable_point -> float -> float -> unit = <fun>
```

```ocaml
# let mypoint = {x = 0.0; y = 0.0};;
val mypoint : mutable_point = {x = 0.; y = 0.}
```

```ocaml
# translate mypoint 1.0 2.0;;
- : unit = ()
```

```ocaml
# mypoint;;
- : mutable_point = {x = 1.; y = 2.}
```

OCaml 没有内建的变量概念，值的标识符是可以通过赋值改变的。（`let` 绑定不是赋值，它在新的作用域中引入了新的标识符。）但是，标准库提供了引用，它们可以间接地被改变，可以使用 `!` 操作符来解引用，`:=` 操作符来赋值。因此可以通过 `let` 绑定来模拟变量。例如，下面是对数组的原位插入排序：

```ocaml
# let insertion_sort a =
    for i = 1 to Array.length a - 1 do
      let val_i = a.(i) in
      let j = ref i in
      while !j > 0 && val_i < a.(!j - 1) do
        a.(!j) <- a.(!j - 1);
        j := !j - 1
      done;
      a.(!j) <- val_i
    done;;
val insertion_sort : 'a array -> unit = <fun>
```

引用在编写有副作用的函数时非常有用的。例如，下面是一个伪随机数生成器，它将最后返回的数组保留在引用中：

```ocaml
# let current_rand = ref 0;;
val current_rand : int ref = {contents = 0}
```

```ocaml
# let random () =
    current_rand := !current_rand * 25713 + 1345;;
val random : unit -> int = <fun>
```

同样，引用并没有什么魔法：它们被实现为单字段的可变记录，如下所示。

```ocaml
# type 'a ref = {mutable contents: 'a};;
type 'a ref = { mutable contents : 'a }
```

```ocaml
# let ( ! ) r = r.contents;;
val ( ! ) : 'a ref -> 'a = <fun>
```

```ocaml
# let ( := ) r newval = r.contents <- newval;
val ( := ) : 'a ref -> 'a -> unit = <fun>
```

在某些特殊情况下，可能需要将多态函数存储在数据结构中，以保持其多态性。执行此操作需用用户提供的类型注解，因为只有对全局定义才会自动引入多态。但是，可以显式地为记录字段指定多态类型。

```ocaml
# type idref = {mutable id: 'a. 'a -> 'a};;
type idref = { mutable id : 'a. 'a -> 'a }
```

```ocaml
# let r = {id = fun x -> x};;
val r : idref = {id = <fun>}
```

```ocaml
# let g s = (s.id 1, s.id true);;
val g : idref -> int * bool = <fun>
```

```ocaml
# r.id <- (fun x -> print_string "called id\n"; x);;
- : unit = ()
```

```ocaml
# g r;;
called id
called id
- : int * bool = (1, true)
```

---

# 异常

OCaml 为抛出和处理异常提供了相应的机制。异常也可以用作通用非局部的控制结构，但不应该过度使用，因为它会使代码变得难以理解。可以使用 `excpet` 声明异常，使用 `raise` 操作符抛出异常信号。例如，下面的函数在匹配列表的头部时，如果列表为空则抛出异常信号。

```ocaml
# exception Empty_list;;
exception Empty_list
```

```ocaml
# let head l =
    match l with
      [] -> raise Empty_list
    | hd :: tl -> hd;;
val head : 'a list -> 'a = <fun>
```

```ocaml
# head [1; 2];;
- : int = 1
```

```ocaml
# head [];;
Exception: Empty_list.
```

整个标准库都使用异常来表示库函数无法正常处理的情况。例如，`List.assoc` 函数在键值对的列表中查询指定键的值，如果在列表中没有相应的键，则抛出 `Not_found` 异常：

```ocaml
# List.assoc 1 [(0, "zero"); (1, "one")];;
- : string = "one"
```

```ocaml
# List.assoc 2 [(0, "zero"); (1, "one")];;
Exception: Not_found
```

可以使用 `try...with` 来捕获异常：

```ocaml
# let name_of_binary_digit digit =
    try
      List.assoc digit [0, "zero"; 1, "one"]
    with Not_found ->
      "not a binary digit";;
val name_of_binary_digit : int -> string = <fun>
```

```ocaml
# name_of_binary_digit 0;;
- : string = "zero"
```

```ocaml
# name_of_binary_digit (-1);;
- : string = "not a binary digit"
```

`with` 部分可以使用与 `match` 相同的语法和行为对异常值进行模式匹配。因此，一次 `try...with` 可以捕获多个异常：

```ocaml
# let rec first_named_value values names =
    try
      List.assoc (head values) names
    with
    | Empty_list -> "no named value"
    | Not_found -> first_named_value (List.tl values) names;;
val first_named_value : 'a list -> ('a * string) list -> string = <fun>
```

```ocaml
# first_named_value [0; 10] [1, "one"; 10, "ten"];;
- : string = "ten"
```

在程序中处理异常时，可以通过捕获所有异常来确保一定的清理或结束操作能够执行，然后在执行完这些操作后重新抛出异常：

```ocaml
# let temporarily_set_reference ref newval funct =
    let oldval = !ref in
    try
      ref := newval
      let res = funct () in
      ref := oldval
      res
    with x ->
      ref := oldval
      raise x;;
val temporarily_set_reference : 'a ref -> 'a -> (unit -> 'b) -> 'b = <fun>
```

可以在模式匹配时捕获异常来代替 `try...with`：

```ocaml
# let assoc_may_map f x l =
    match List.assoc x l with
    | exception Not_found -> None
    | y -> f y;;
val assoc_may_map : ('a -> 'b option) -> 'c -> ('c * 'a) list -> 'b option = <fun>
```

需要注意的是，此结构仅在 `match...with` 之间会抛出异常时才有用。异常模式可以与顶层的普通模式组合，但它们不能嵌套在其他模式中，例如模式 `Some (execption A)` 是非法的。

```ocaml
# let flat_assoc_opt x l =
    match List.assoc x l with
    | None | exception Not_found -> None
    | Some _ as v -> v;;
val flat_assoc_opt : 'a -> ('a * 'b option) list -> 'b option = <fun>
```

当使用异常作为控制结构时，最好将异常的作用范围限制在尽可能小的范围内。例如，由于函数 `f` 不会抛出 `Done` 异常，这避免了一整类的错误情况。

```ocaml
# let fixpoint f x =
    let exception Done in
    let x = ref x in
    try while true do
      let y = f !x in
      if !x = y then raise Done else x := y
    done
    with Done -> !x;;
val fixpoint : ('a -> 'a) -> 'a -> 'a = <fun>
```

---

# 惰性表达式

OCaml 允许将一些计算推迟到真正需要的时候。

可以使用 `lazy (expr)` 来延迟表达式 `expr` 的计算。例如，我们可以推迟 `1 + 1` 的计算，直到我们需要该表达式的结果 `2`。让我们看看如何初始化惰性表达式。

```ocaml
# let lazy_two = lazy (print_endline "lazy_two evalution"; 1 + 1);;
val lazy_two : int lazy_t = <lazy>
```

我们添加了 `print_endline "lazy_two evalution"` 来查看惰性表达式何时被求值。

`lazy_two` 的值显示为 `<lazy>`，这意味着该表达式尚未计算，其最终值未知。

需要注意的是，`lazy_two` 的类型是 `int lazy_t`。但是，`'a lazy_t` 类型是内部的类型名称，所以在尽可能的情况下会首先 `'a Lazy.t` 类型。

当我们最终需要惰性表达式的结果时，我们可以对该表达式调用 `Lazy.force` 来强制求值。函数 `force` 来自标准库 `Lazy`。

```ocaml
# Lazy.force lazy_two;;
lazy_two evaluation
- : int = 2
```

需要注意的是，上面的函数调用先打印了 `lay_two evaluation`，然会返回计算的值。

现在，如果我们查看 `lazy_two` 的值，我们会发现它不再显示为 `<lazy>`，而是显示为 `lazy 2`。

```ocaml
# lazy_two;;
- : int lazy_t = lazy 2
```

这是因为 `Lazy.force` 会记住表达式的结果。换句话说，对该表达式的每次后续调用 `Lazy.force` 都会返回第一次计算的结果，而无需重新计算惰性表达式。让我们再次对 `lazy_two` 调用 `force`。

```ocaml
# Lazy.force lazy_two;;
- : int = 2
```

可以看到，这次没有打印出 `lazy_two evaluation`，说明这次调用没有重新进行计算，而是简单地返回了初次计算的结果。

惰性模式提供了另一种强制执行惰性表达式的方法。

```ocaml
# let lazy_l = lazy ([1; 2] @ [3; 4]);;
val lazy_l : int list lazy_t = <lazy>
```

```ocaml
# let lazy l = lazy_l;;
val l : int list = [1; 2; 3; 4]
```

我们还可以在模式匹配中使用惰性模式。

```ocaml
# let maybe_eval lazy_guard lazy_expr =
    match lazy_guard, lazy_expr with
    | lazy false, _ ->
      "matches if (Lazy.force lazy_guard = false); lazy_expr not forced"
    | lazy true, lazy _ ->
      "matches if (Lazy.force lazy_guard = true); lazy_expr forced";;
val maybe_eval : bool lazy_t -> 'a lazy_t -> string = <fun>
```

只有当 `lazy_guard` 的值在计算后为 `true` 时，才会强制执行惰性表达式 `lazy_expr`。事实上，简单的通配符模式永远不会强制执行惰性表达式，除非是具有关键字 `lazy` 的模式。

---

# 表达式的符号处理

我们以一个更完整的示例来演示如何使用 OCaml 来处理符号：包含变量的算术表达式的形式化操作。以下变体类型描述了我们将要操作的表达式：

```ocaml
# type expression =
      Const of float
    | Var of string
    | Sum of expression * expression (* e1 + e2 *)
    | Diff of expression * expression (* e1 - e2 *)
    | Prod of expression * expression (* e1 * e2 *)
    | Quot of expression * expression (* e1 / e2 *)
  ;;
type expression =
    Const of float
  | Var of string
  | Sum of expression * expression
  | Diff of expression * expression
  | Prod of expression * expression
  | Quot of expression * expression
```

我们首先定义一个函数来计算给定环境中的表达式，该环境将变量名称映射到它们的值。为了简单起见，环境被表示为关联列表。

```ocaml
# exception Unbound_variable of string;;
exception Unbound_variable of string
```

```ocaml
# let rec eval env exp =
    match exp with
      Const c -> c
    | Var v ->
      (try List.assoc v env with Not_found -> raise (Unbound_variable v))
    | Sum(f, g) -> eval env f +. eval env g
    | Diff(f, g) -> eval env f -. eval env g
    | Prod(f, g) -> eval env f *. eval env g
    | Quot(f, g) -> eval env f /. eval env g;;
val eval : (string * float) list -> expression -> float = <fun>
```

```ocaml
eval [("x", 1.0); ("y", 3.14)] (Prod(Sum(Var "x", Const 2.0), Var "y"));;
- : float = 9.42
```

现在，对于真正的符号处理，我们定义表达式对于变量 `dv` 的导数：

```ocaml
# let rec deriv exp dv =
    match exp with
      Const c -> Const 0.0
    | Var v -> if v = dv then Const 1.0 else Const 0.0
    | Sum(f, g) -> Sum(deriv f dv, deriv g dv)
    | Diff(f, g) -> Diff(deriv f dv, deriv g dv)
    | Prod(f, g) -> Sum(Prod(f, deriv g dv), Prod(deriv f dv, g))
    | Quot(f, g) -> Quot(Diff(Prod(deriv f dv, g), Prod(f, deriv g dv)),
                         Prod(g, g))
  ;;
val deriv : expression -> string -> expression = <fun>
```

```ocaml
# deriv (Quot(Const 1.0, Var "x")) "x";;
- : expression =
Quot (Diff (Prod (Const 0., Var "x"), Prod (Const 1., Const 1.)),
      Prod (Var "x", Var "x"))
```

---

# 更好的打印

在上面的例子中，随着表达式变大，表达式的内部表示（也称为抽象语法）很快就会变得难以读写。我们需要一个打印器和解析器来在抽象语法和具体语法之间来回切换。

对于打印函数，我们考虑通常的优先级规则以避免不必要的括号。为此，我们保持当前的运算符优先级，并且仅当其优先级小于当前优先级时草在运算符周围打印括号。

```ocaml
# let print_expr exp =
    (* Local function definitions *)
    let open_paren prec op_prec =
      if prec > op_prec then print_string "(" in
    let close_paren prec op_prec =
      if prec > op_prec then print_string ")" in
    let rec print prec exp = (* prec is the current precedence *)
      match exp with
        Const c -> print_float c
      | Var v -> print_string v
      | Sum(f, g) ->
          open_paren prec 0;
          print 0 f; print_string " + "; print 0 g;
          close_paren prec 0
      | Diff(f, g) ->
          open_paren prec 0;
          print 0 f; print_string " - "; print 1 g;
          close_paren prec 0
      | Prod(f, g) ->
          open_paren prec 2;
          print 2 f; print_string " * "; print 2 g;
          close_paren prec 2
      | Quot(f, g) ->
          open_paren prec 2;
          print 2 f; print_string " / "; print 3 g;
          close_paren prec 2
    in print 0 expr;;
val print_expr : expression -> unit = <fun>
```

```ocaml
# let e = Sum(Prod(Const 2.0, Var "x"), Const 1.0);;
val e : expression = Sum (Prod (Const 2., Var "x"), Const 1.)
```

```ocaml
# print_expr e; print_newline ();;
2. * x + 1.
- : unit = ()
```

```ocaml
# print_expr (deriv e "x"); print_newline ();;
2. * 1. + 0. * x + 0.
- : unit = ()
```

---

# 格式化输出

`Printf` 模块中有一个 `printf` 函数，可以更简洁地进行格式化输出。它遵循 C 标准库中 `printf` 函数的行为。

```ocaml
# Printf.printf "%i + %i is an integer value, %F * %F is a float, %s\n"
  3 2 4.5 1. "this is a string";;
3 + 2 is an integer value, 4.5 * 1. is a float, "this is a string"
- : unit = ()
```

OCaml 类型系统会检查参数的类型和说明符是否兼容。如果传递了一个与格式说明符类型不对应的参数，编译器则会显示一条错误信息：

```ocaml
# Printf.printf "Float value: %F" 42;;
Error: This expression has type int but an expression was expected of
       type float
  Hint: Did you mean 42.?
```

`fprintf` 函数与 `printf` 类似，只是它接收输出通道作为第一个参数。`%a` 说明符对于自定义打印器非常有用。例如，我们可以创建一个打印模板，将整数参数转换为有符号十进制数：

```ocaml
# let pp_int ppf n = Printf.fprintf ppf "%d" n;;
val pp_int : out_channel -> int -> unit = <fun>
```

```ocaml
# Printf.printf "Outputting an integer using a custom printer: %a" pp_int 42;;
Outputting an integer using a custom printer: 42 - : unit = ()
```

这些基于 `%a` 说明符的打印器的优点是它们可以组合在一起以逐步创建更复杂的打印器。我们可以定义一个组合器，可以将 `'a type` 的打印器转换为 `'a optional` 的打印器：

```ocaml
# let pp_option printer ppf = function
    | None -> Printf.fprintf ppf "None"
    | Some v -> Printf.fprintf ppf "Some(%a)" printer v;;
val pp_option :
  (out_channel -> 'a -> unit) -> out_channel -> 'a option -> unit = <fun>
```

```ocaml
# Printf.fprintf stdout
    "The current setting is %a. \nThere is only %a\n"
    (pp_option pp_int) (Some 3)
    (pp_option pp_int) None
  ;;
The current setting is Some(3).
There is only None
- : unit = ()
```

如果其参数的值为 `None`，则 `pp_option` 会打印 `None`，否则它将使用提供的打印器打印 `Some`。

以下是如何使用 `fprintf` 重写上一节中的打印器：

```ocaml
# let pp_expr ppf expr =
    let open_paren prec op_prec output =
      if prec > op_prec then Printf.fprintf "%s" "(" in
    let close_paren prec op_prec output =
      if prec > op_prec then Printf.fprintf "%s" ")" in
    let rec print prec ppf expr =
      match expr with
        Const c -> Printf.fprintf ppf "%F" c
      | Var v -> Printf.fprintf ppf "%s" v
      | Sum(f, g) ->
          open_paren prec 0 ppf;
          Printf.fprintf ppf "%a + %a" (print 0) f (print 0) g;
          close_paren prec 0 ppf
      | Diff(f, g) ->
          open_paren prec 0 ppf;
          Printf.fprintf ppf "%a - %a" (print 0) f (print 1) g;
          close_paren prec 0 ppf
      | Prod(f, g) ->
          open_paren prec 2 ppf;
          Printf.fprintf ppf "%a * %a" (print 2) f (print 2) g;
          close_paren prec 2 ppf
      | Quot(f, g) ->
          open_paren prec 2 ppf;
          Printf.fprintf ppf "%a / %a" (print 2) f (print 3) g;
          close_paren prec 2 ppf
    in print 0 ppf expr;;
val pp_expr : out_channel -> expression -> unit = <fun>
```

```ocaml
# pp_expr stdout e; print_newline ();;
2. * x + 1.
- : unit = ()
```

```ocaml
# pp_expr stdout (deriv e "x"); print_newline ();;
2. * 1. + 0. * x + 0.
- : unit = ()
```

由于格式化字符串的构建方式，存储格式化字符串时需要显示标注类型：

```ocaml
# let str : _ format =
    "%i is an integer value, %F is a float, %S\n";;
```

```ocaml
# Printf.printf str 3 4.5 "string value";;
3 is an integer value, 4.5 is a float, "string value"
- : unit = ()
```

---

# 独立的 OCaml 程序

到目前为止的所有示例都是在交互式系统下执行的。OCaml 代码还可以使用编译器 `ocamlc` 和 `ocamlopt` 单独编译并以非交互式执行。OCaml 的源代码必须放在扩展名为 `.ml` 的文件中。它由一系列短语组成，这些短语将在运行时按照它们在源文件中出现的顺序执行。与交互模式不同，类型和值并不会自动打印出来；程序必须显式地调用打印函数来产生一些输出。交互模式中使用的 `;;` 在源文件中是不需要的，但对于标记顶级表达式的结尾是很有用的。下面是一个示例独立程序，用于打印两个数组的最大公约数：

```ocaml file:gcd.ml
let rec gcd a b =
  if b = 0 then a
  else gcd b (a mod b);;

let main () =
  let a = int_of_string Sys.argv.(1) in
  let b = int_of_string Sys.argv.(2) in
  Printf.printf "%d\n" (gcd a b);
  exit 0;;
main ();;
```

`Sys.argv` 是命令行参数的字符串数组。因此，`Sys.argv(1)` 是第一个命令行参数。上面的程序是使用以下 shell 命令编译和执行的：

```bash
$ ocamlc -o gcd gcd.ml
$ ./gcd 6 9
3
$ ./gcd 7 11
1
```
